Blurry Spruce Weasel

Medium

# Incentive contracts can not be initialized with Fee on Transfer tokens

### Summary

Transfer fee is not accounted when `disburse`ing from `ManagedBudget` to incentive contracts will cause incentive contracts can not be initialized with Fee on Transfer tokens.

### Root Cause

All the incentive contracts for distributing ERC20 tokens (`ERC20Incentive`, `ERC20VariableIncentive`, `CGDAIncentive`) are not accounted for transfer fee on initialization.

<details>
  <summary>The `ERC20Incentive` contract</summary>

`init_.reward * init_.limit` (or `init_.reward`) tokens is disbursed to `ERC20Incentive`

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L68

```solidity
    function preflight(bytes calldata data_) external view override returns (bytes memory budgetData) {
        InitPayload memory init_ = abi.decode(data_, (InitPayload));
>>      uint256 amount = init_.strategy != Strategy.RAFFLE ? init_.reward * init_.limit : init_.reward;

        return abi.encode(
            ABudget.Transfer({
                assetType: ABudget.AssetType.ERC20,
                asset: init_.asset,
                target: address(this),
>>              data: abi.encode(ABudget.FungiblePayload({amount: amount}))
            })
        );
    }
```

`init_.reward * init_.limit` (or `init_.reward`) tokens is checked against the contract's balance

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L44

```solidity
    function initialize(bytes calldata data_) public override initializer {
        InitPayload memory init_ = abi.decode(data_, (InitPayload));

        if (init_.reward == 0 || init_.limit == 0) revert BoostError.InvalidInitialization();

        // Ensure the maximum reward amount has been allocated
>>      uint256 maxTotalReward = init_.strategy != Strategy.RAFFLE ? init_.reward * init_.limit : init_.reward;
        uint256 available = init_.asset.balanceOf(address(this));
>>      if (available < maxTotalReward) {
            revert BoostError.InsufficientFunds(init_.asset, available, maxTotalReward);
        }

        asset = init_.asset;
        strategy = init_.strategy;
        reward = init_.reward;
        limit = init_.limit;
        _initializeOwner(msg.sender);
    }
```
</details>

<details>
  <summary>The `ERC20VariableIncentive` contract</summary>

`limit_` tokens is disbursed to `ERC20VariableIncentive`

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/incentives/ERC20VariableIncentive.sol#L124

```solidity
    function preflight(bytes calldata data_) external view override returns (bytes memory budgetData) {
        // TODO: remove unused reward param
>>      (address asset_, uint256 reward_, uint256 limit_) = abi.decode(data_, (address, uint256, uint256));

        return abi.encode(
            ABudget.Transfer({
                assetType: ABudget.AssetType.ERC20,
                asset: asset_,
                target: address(this),
>>              data: abi.encode(ABudget.FungiblePayload({amount: limit_}))
            })
        );
    }
```

`limit_` tokens is checked against the contract's balance

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/incentives/ERC20VariableIncentive.sol#L47

```solidity
    function initialize(bytes calldata data_) public override initializer {
        _initializeOwner(msg.sender);
        InitPayload memory init_ = abi.decode(data_, (InitPayload));

        address asset_ = init_.asset;
        uint256 reward_ = init_.reward;
>>      uint256 limit_ = init_.limit;

        if (limit_ == 0) revert BoostError.InvalidInitialization();

        uint256 available = asset_.balanceOf(address(this));
>>      if (available < limit_) {
            revert BoostError.InsufficientFunds(init_.asset, available, limit_);
        }

        asset = asset_;
        reward = reward_;
        limit = limit_;
        totalClaimed = 0;

        _initializeOwner(msg.sender);
    }
```

</details>

<details>
  <summary>The `CGDAIncentive` contract</summary>

`init_.totalBudget` tokens is is disbursed to `CGDAIncentive`

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/incentives/CGDAIncentive.sol#L78

```solidity
    function preflight(bytes calldata data_) external view virtual override returns (bytes memory) {
        InitPayload memory init_ = abi.decode(data_, (InitPayload));

        return abi.encode(
            ABudget.Transfer({
                assetType: ABudget.AssetType.ERC20,
                asset: init_.asset,
                target: address(this),
>>              data: abi.encode(ABudget.FungiblePayload({amount: init_.totalBudget}))
            })
        );
    }
```

`init_.totalBudget` tokens is checked against the contract's balance

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/incentives/CGDAIncentive.sol#L45

```solidity
    function initialize(bytes calldata data_) public override initializer {
        InitPayload memory init_ = abi.decode(data_, (InitPayload));

        uint256 available = init_.asset.balanceOf(address(this));
>>      if (available < init_.totalBudget) {
            revert BoostError.InsufficientFunds(init_.asset, available, init_.totalBudget);
        }

        if (
            init_.initialReward == 0 || init_.rewardDecay == 0 || init_.rewardBoost == 0
                || init_.totalBudget < init_.initialReward
        ) revert BoostError.InvalidInitialization();

        asset = init_.asset;
        cgdaParams = CGDAParameters({
            rewardDecay: init_.rewardDecay,
            rewardBoost: init_.rewardBoost,
            lastClaimTime: block.timestamp,
            currentReward: init_.initialReward
        });

        totalBudget = init_.totalBudget;
        _initializeOwner(msg.sender);
    }
```
</details>

When transferring `X` Fee on Transfer tokens from contract `A` to contract `B`, contract `B` will receive less than `X` tokens. This behavior of Fee on Transfer tokens will cause the check on initialization always fails.


### Internal pre-conditions

Fee on Transfer token is used in an incentive contract.

### External pre-conditions

_No response_

### Attack Path

The boost creator creates a boost with Fee on Transfer tokens.

### Impact

Incentive contracts can not be initialized, which will cause the boost can not be created.

### PoC

_No response_

### Mitigation

We believe the check against the contract's balance could be excluded from the initialization. The `reward` variable should be set to the contract's balance.