Genuine Brown Barbel

Medium

# Excess ETH Routed to Protocol Instead of Refund in `claimIncentiveFor` Function

## Summary
In the `BoostCore` contract, the `claimIncentiveFor` function does not refund excess ETH sent in the transaction if `msg.value` is greater than the required `claimFee`. The excess ETH is mistakenly routed to the `protocolFeeReceiver`, resulting in potential overpayment by boost participants without a refund mechanism.

## Vulnerability Detail
The function `claimIncentiveFor` checks if `msg.value` is less than `claimFee` and reverts if insufficient funds are sent:
```solidity
    function claimIncentiveFor(
        uint256 boostId_,
        uint256 incentiveId_,
        address referrer_,
        bytes calldata data_,
        address claimant
    ) public payable nonReentrant {
        BoostLib.Boost storage boost = _boosts[boostId_];
@>      if (msg.value < claimFee) revert BoostError.InsufficientFunds(address(0), msg.value, claimFee);
        _routeClaimFee(boost, referrer_);

        // wake-disable-next-line reentrancy (false positive, function is nonReentrant)
        if (!boost.validator.validate(boostId_, incentiveId_, claimant, data_)) revert BoostError.Unauthorized();
        if (!boost.incentives[incentiveId_].claim(claimant, data_)) {
            revert BoostError.ClaimFailed(claimant, data_);
        }
    }
``` 
However, there is no logic to refund excess ETH if `msg.value` exceeds the required `claimFee`. The `_routeClaimFee` function simply transfers the entire balance of the contract (including any excess ETH) to the `protocolFeeReceiver`, which may lead to boost participants overpaying unintentionally:
```solidity
    function _routeClaimFee(BoostLib.Boost storage boost, address referrer_) internal {
        if (claimFee == 0) return;
        uint256 netFee = claimFee;

        // If a referrer is provided, transfer the revshare and reduce the net fee
        if (referrer_ != address(0)) {
            uint256 referralShare = claimFee * boost.referralFee / FEE_DENOMINATOR;
            netFee -= referralShare;
            referrer_.safeTransferETH(referralShare);
        }

        // The remaining fee is split between the owner and the protocol
        boost.owner.safeTransferETH(netFee / 2);
@>      protocolFeeReceiver.safeTransferETH(address(this).balance);
    }
```

## Impact
Boost participants who send more ETH than the `claimFee` while claiming incentives will overpay without receiving a refund for the excess amount. This could lead to a loss of funds for participants and negatively impact their experience with the contract. The excess ETH is misdirected to the protocol instead of being refunded to the participant.

## Code Snippet
https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/d9f597776cc2d20fbb19ffb1f7731126cf3b6210/boost-protocol/packages/evm/contracts/BoostCore.sol#L164-L180

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/d9f597776cc2d20fbb19ffb1f7731126cf3b6210/boost-protocol/packages/evm/contracts/BoostCore.sol#L305-L319

## Tool used
Manual Review

## Recommendation
Implement a refund mechanism in the `claimIncentiveFor` function that refunds any excess ETH sent beyond the `claimFee` back to the boost participant:
```diff
   function claimIncentiveFor(
        uint256 boostId_,
        uint256 incentiveId_,
        address referrer_,
        bytes calldata data_,
        address claimant
    ) public payable nonReentrant {
        BoostLib.Boost storage boost = _boosts[boostId_];
        if (msg.value < claimFee) revert BoostError.InsufficientFunds(address(0), msg.value, claimFee);
      
         // Refund excess ETH
+       if (msg.value > claimFee) {
+           msg.sender.safeTransferETH(msg.value - claimFee);
+       }

        _routeClaimFee(boost, referrer_);

        // wake-disable-next-line reentrancy (false positive, function is nonReentrant)
        if (!boost.validator.validate(boostId_, incentiveId_, claimant, data_)) revert BoostError.Unauthorized();
        if (!boost.incentives[incentiveId_].claim(claimant, data_)) {
            revert BoostError.ClaimFailed(claimant, data_);
        }
    }
```