Docile Cerulean Zebra

Medium

# The protocol is not compatible with FOT tokens

### Summary

The protocol is not compatible with FOT tokens, causing some incentives cannot be deployed.

### Root Cause

There are a few places where FOT tokens can cause problems, some are minor, but some are quite severe. First, in [`ManagedBudget::allocate`](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/budgets/ManagedBudget.sol#L66C1-L74C11), we see for the ERC20 assets allocation, token balance is compared with `payload.amount`:
```solidity
        } else if (request.assetType == AssetType.ERC20) {
            FungiblePayload memory payload = abi.decode(request.data, (FungiblePayload));

            // Transfer `payload.amount` of the token to this contract
            request.asset.safeTransferFrom(request.target, address(this), payload.amount);
            if (request.asset.balanceOf(address(this)) < payload.amount) {
                revert InvalidAllocation(request.asset, payload.amount);
            }
        } 
```

For the first allocation of FOT ERC20, this would revert, as some portion will be deducted as fee, but this is fine, as allocators can always "donate" some before allocation to partially solve this issue. But for incentive creation, this cannot be solved easily, as we see in `BoostCore::_makeIncentives`:

```solidity
    function _makeIncentives(BoostLib.Target[] memory targets_, ABudget budget_)
        internal
        returns (AIncentive[] memory incentives)
    {
        incentives = new AIncentive[](targets_.length);
        for (uint256 i = 0; i < targets_.length; i++) {
            // Deploy the clone, but don't initialize until it we've preflighted
            _checkTarget(type(AIncentive).interfaceId, targets_[i].instance);

            // Ensure the target is a base implementation (incentive clones are not reusable)
            if (!targets_[i].isBase) {
                revert BoostError.InvalidInstance(type(AIncentive).interfaceId, targets_[i].instance);
            }

            incentives[i] = AIncentive(_makeTarget(type(AIncentive).interfaceId, targets_[i], false));

            bytes memory preflight = incentives[i].preflight(targets_[i].parameters);
            if (preflight.length != 0) {
                // wake-disable-next-line reentrancy (false positive, entrypoint is nonReentrant)
                assert(budget_.disburse(preflight));
            }

            // wake-disable-next-line reentrancy (false positive, entrypoint is nonReentrant)
            incentives[i].initialize(targets_[i].parameters);
        }
    }
```
which calls `budget_.disbuse`, and the call data, is specified by `preflight` result. For example, this is from `CGDAIncentive::preflight`:
```solidity
    function preflight(bytes calldata data_) external view virtual override returns (bytes memory) {
        InitPayload memory init_ = abi.decode(data_, (InitPayload));

        return abi.encode(
            ABudget.Transfer({
                assetType: ABudget.AssetType.ERC20,
                asset: init_.asset,
                target: address(this),
                data: abi.encode(ABudget.FungiblePayload({amount: init_.totalBudget}))
            })
        );
    }
```
Where the transferred amount is set to `totalBudget`, and in `disburse`, we see:
```solidity
    function disburse(bytes calldata data_)
        public
        virtual
        override
        onlyOwnerOrRoles(ADMIN_ROLE | MANAGER_ROLE)
        returns (bool)
    {
        Transfer memory request = abi.decode(data_, (Transfer));
        if (request.assetType == AssetType.ERC20 || request.assetType == AssetType.ETH) {
            FungiblePayload memory payload = abi.decode(request.data, (FungiblePayload));

            uint256 avail = available(request.asset);
            if (payload.amount > avail) {
                revert InsufficientFunds(request.asset, avail, payload.amount);
            }

            _transferFungible(request.asset, request.target, payload.amount);
        } else if (request.assetType == AssetType.ERC1155) {
            ERC1155Payload memory payload = abi.decode(request.data, (ERC1155Payload));

            uint256 avail = IERC1155(request.asset).balanceOf(address(this), payload.tokenId);
            if (payload.amount > avail) {
                revert InsufficientFunds(request.asset, avail, payload.amount);
            }

            _transferERC1155(request.asset, request.target, payload.tokenId, payload.amount, payload.data);
        } else {
            return false;
        }

        return true;
    }
```

This `totalBudget` is used for transfer amount, eventually, `incentive::initialize` is called, again, using `CGDAIncentive` as example:
```solidity
    function initialize(bytes calldata data_) public override initializer {
        InitPayload memory init_ = abi.decode(data_, (InitPayload));

        uint256 available = init_.asset.balanceOf(address(this));
        if (available < init_.totalBudget) {
            revert BoostError.InsufficientFunds(init_.asset, available, init_.totalBudget);
        }

        if (
            init_.initialReward == 0 || init_.rewardDecay == 0 || init_.rewardBoost == 0
                || init_.totalBudget < init_.initialReward
        ) revert BoostError.InvalidInitialization();

        asset = init_.asset;
        cgdaParams = CGDAParameters({
            rewardDecay: init_.rewardDecay,
            rewardBoost: init_.rewardBoost,
            lastClaimTime: block.timestamp,
            currentReward: init_.initialReward
        });

        totalBudget = init_.totalBudget;
        _initializeOwner(msg.sender);
    }
```

We see that if balance is less than `totalBudget`, it will revert, and this would exactly happen when the token is FOT, causing deployment of incentive contract to revert.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

When FOT tokens are used for some incentive programs, it will fail due to revert.

### PoC

_No response_

### Mitigation

Either don't support FOT tokens at all, or add additional supports for FOT tokens.