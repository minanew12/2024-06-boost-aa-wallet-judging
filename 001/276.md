Powerful Onyx Urchin

Medium

# The protocol won't work correctly with fee on transfer tokens

### Summary

The Boost Core protocol claims to support all [weird tokens](https://github.com/d-xo/weird-erc20). As per the Readme:
**Protocol should work with all native and ERC20 tokens that adhear to standard including weird tokens.
No assumptions are made in regards to the properties of integrating tokens and weird trait tokens should be handled normally. Specifically we'd expect to interact with USDT and other stable coins/decimal 6 coins and erc20z from Zora as well as other fractional NFT contracts as long as they adhere to ERC20 standard. We aren't whitelisting tokens so any erc20 should work (including weird ones).** However there will be a problem if users want to create a boost with different ERC20 Incentives contracts and they use a fee on transfer token as the reward token. Users can create boosts via the [createBoost()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L106-L144) function, the [createBoost()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L106-L144) function calls many different functions internally, but the one responsible for setting up the incentives is the [_makeIncentives()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L266-L291) function. 
```solidity
    function _makeIncentives(BoostLib.Target[] memory targets_, ABudget budget_)
        internal
        returns (AIncentive[] memory incentives)
    {
        incentives = new AIncentive[](targets_.length);
        for (uint256 i = 0; i < targets_.length; i++) {
            // Deploy the clone, but don't initialize until it we've preflighted
            _checkTarget(type(AIncentive).interfaceId, targets_[i].instance);

            // Ensure the target is a base implementation (incentive clones are not reusable)
            if (!targets_[i].isBase) {
                revert BoostError.InvalidInstance(type(AIncentive).interfaceId, targets_[i].instance);
            }

            incentives[i] = AIncentive(_makeTarget(type(AIncentive).interfaceId, targets_[i], false));

            bytes memory preflight = incentives[i].preflight(targets_[i].parameters);
            if (preflight.length != 0) {
                // wake-disable-next-line reentrancy (false positive, entrypoint is nonReentrant)
                assert(budget_.disburse(preflight));
            }

            // wake-disable-next-line reentrancy (false positive, entrypoint is nonReentrant)
            incentives[i].initialize(targets_[i].parameters);
        }
    }
```
As can be seen from the above code snippet, first some checks are performed then a minimal proxy of the provided implementation is created.  However before the Incentives contract is initialized the protocol calls the **preflight()** function on the corresponding Incentives clone in order to determine which tokens, and how much of it to disburse from the budged first. Let's consider the ``ERC20Incentive.sol`` contract, the [preflight()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L59-L71) function is as follows:
```solidity
    function preflight(bytes calldata data_) external view override returns (bytes memory budgetData) {
        InitPayload memory init_ = abi.decode(data_, (InitPayload));
        uint256 amount = init_.strategy != Strategy.RAFFLE ? init_.reward * init_.limit : init_.reward;

        return abi.encode(
            ABudget.Transfer({
                assetType: ABudget.AssetType.ERC20,
                asset: init_.asset,
                target: address(this),
                data: abi.encode(ABudget.FungiblePayload({amount: amount}))
            })
        );
    }
```

As can be seen from the above code snippet if the set strategy for the incentive is not **RAFFLE** the amount will be calculated by multiplying the **init_.reward** by the **init_.limit**. And this amount will have to be sent to the instance, before the contract is initialized via the [ERC20Incentive::initialize()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L36-L53) function:
```solidity
    function initialize(bytes calldata data_) public override initializer {
        InitPayload memory init_ = abi.decode(data_, (InitPayload));

        if (init_.reward == 0 || init_.limit == 0) revert BoostError.InvalidInitialization();

        // Ensure the maximum reward amount has been allocated
        uint256 maxTotalReward = init_.strategy != Strategy.RAFFLE ? init_.reward * init_.limit : init_.reward;
        uint256 available = init_.asset.balanceOf(address(this));
        if (available < maxTotalReward) {
            revert BoostError.InsufficientFunds(init_.asset, available, maxTotalReward);
        }

        asset = init_.asset;
        strategy = init_.strategy;
        reward = init_.reward;
        limit = init_.limit;
        _initializeOwner(msg.sender);
    }
``` 
As can be seen from the above code snippet **maxTotalReward** will be equal to **init_.reward * init_.limit**. This are the same calculations for amount that were performed by the code in the [preflight()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L59-L71) function. When the [ManagedBudget::disburse()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/budgets/ManagedBudget.sol#L127-L158) function is called from the [_makeIncentives()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L266-L291) function with the amount calculated in the [preflight()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L59-L71) function and the tokens are fee on trasnfer tokens the amount that will end up in the incentive contract will be less. Lets say for example the  [preflight()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L59-L71) function calculates an amount of 100e18 tokens, but the tokens have 10% fee on transfer, meaning that the receiver will receive only 90e18 tokens. When we go back to the [ERC20Incentive::initialize()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L36-L53) function the balance of the contract will be 90e18 but the **maxTotalReward** will be 100e18 and the call will revert because of the follwoing check:
```solidity
        if (available < maxTotalReward) {
            revert BoostError.InsufficientFunds(init_.asset, available, maxTotalReward);
        }

```

### Root Cause
The contract doesn't support functionality to successfully initialize incentives contracts with fee on transfer tokens. 

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact
The users of the protocol won't be able to deploy incentive boost with fee on transfer tokens. Given the fact that creating incentive boosts is the core functionality of the protocol, this is breaking a core functionality of the protocol thus the medium severity. 

### PoC

[Gist](https://gist.github.com/AtanasDimulski/f348a8aae7e5cc509d4cbb952cd5ea47)
After following the steps from the above mentioned [gist](https://gist.github.com/AtanasDimulski/f348a8aae7e5cc509d4cbb952cd5ea47) add the following lines to the ``AuditorTests.t.sol`` file:

```solidity
    function test_FeeOnTransferBoost() public {
        vm.startPrank(alice);
        mockFeeOnTransfer.mint(alice, 200e18);
        managedBudgetImplementation = new ManagedBudget();
        managedBudgetClone = ManagedBudget(payable(address(managedBudgetImplementation).clone()));
        mockFeeOnTransfer.transfer(address(managedBudgetClone), 200e18);

        address[] memory authorize = new address[](1);
        authorize[0] = address(boostCore);
        uint256[] memory roles = new uint256[](1);
        roles[0] = 1;

        managedBudgetClone.initialize(
            abi.encode(
                ManagedBudget.InitPayload({owner: alice, authorized: authorize, roles: roles})
            )
        );

        BoostLib.Target memory action = _makeAction(address(mockERC721), MockERC721.mint.selector, mockERC721.mintPrice());
        BoostLib.Target memory allowList = _makeAllowList(alice, alice);

        bytes memory validCreateCalldata = LibZip.cdCompress(
            abi.encode(
                BoostCore.InitPayload({
                    budget: managedBudgetClone,
                    action: action,
                    validator: BoostLib.Target({isBase: true, instance: address(0), parameters: ""}),
                    allowList: allowList,
                    incentives: _makeIncentives(1, address(mockFeeOnTransfer)),
                    protocolFee: 500, // 5%
                    referralFee: 1_000, // 10%
                    maxParticipants: 10_000,
                    owner: alice
                })
            )
        );

        vm.expectRevert();
        boostCore.createBoost(validCreateCalldata);  
        vm.stopPrank();
    }
```

To run the test use: ``forge test -vvv --mt test_FeeOnTransferBoost``

### Mitigation

_No response_