Immense Cream Trout

High

# Incorrect distribution of `claimFee` in `BoostCore::_routeClaimFee`

## Summary

The BoostCore contract contains a critical flaw in its `_routeClaimFee` function, leading to incorrect distribution of claim fees among the boost owner, referrer, and protocol fee receiver. This results in the boost owner receiving less than their intended share, while the protocol fee receiver obtains an excess amount.

## Vulnerability Detail

The vulnerability lies in the logic of the `_routeClaimFee` function below. Instead of calculating the protocol's share based on the `boost.protocolFee`, the function incorrectly sends the entire remaining balance to the protocol fee receiver after deducting the referral fee and half of the net fee for the boost owner.

```solidity
function _routeClaimFee(BoostLib.Boost storage boost, address referrer_) internal {
    if (claimFee == 0) return;
    uint256 netFee = claimFee;

    // If a referrer is provided, transfer the revshare and reduce the net fee
    if (referrer_ != address(0)) {
        uint256 referralShare = claimFee * boost.referralFee / FEE_DENOMINATOR;
        netFee -= referralShare;
        referrer_.safeTransferETH(referralShare);
    }

    // The remaining fee is split between the owner and the protocol
    boost.owner.safeTransferETH(netFee / 2);
    //@audit should use boost.protocolFee not just send all the remainder to the protocol
    protocolFeeReceiver.safeTransferETH(address(this).balance);
}
```

Here's a breakdown of the flawed logic:

1. The referral fee is correctly calculated and deducted if a referrer is provided.
2. Half of the remaining fee is sent to the boost owner.
3. The entire remaining balance is sent to the protocol fee receiver, regardless of the actual `boost.protocolFee` protocol fee percentage.

This approach disregards the intended `boost.protocolFee` protocol fee percentage set at the boost creation phase and results in an unfair distribution of fees.

Example scenario:
- Claim Fee: 1 ETH
- Referral Fee: 10% (0.1 ETH)
- Protocol Fee: 10% (should be 0.1 ETH)

Expected distribution:
- Referrer: 0.1 ETH
- Protocol Fee Receiver: 0.1 ETH
- Boost Owner: 0.8 ETH

Actual distribution due to the bug:
- Referrer: 0.1 ETH
- Boost Owner: 0.45 ETH
- Protocol Fee Receiver: 0.45 ETH

## Impact

Financial loss for boost owners as they will receive substantially less than their intended share of the claim fee.

## Code Snippet

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L121-L122

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L305-L319

## Tool used

Manual Review

## Recommendation

To fix this vulnerability, the `_routeClaimFee` function should be modified to correctly calculate and distribute the protocol fee based on the `boost.protocolFee` value. Here's the recommended implementation:

```solidity
function _routeClaimFee(BoostLib.Boost storage boost, address referrer_) internal {
    if (claimFee == 0) return;
    uint256 netFee = claimFee;

    // If a referrer is provided, transfer the revshare and reduce the net fee
    if (referrer_ != address(0)) {
        uint256 referralShare = claimFee * boost.referralFee / FEE_DENOMINATOR;
        netFee -= referralShare;
        referrer_.safeTransferETH(referralShare);
    }

    // Calculate the protocol's share
    uint256 protocolShare = netFee * boost.protocolFee / FEE_DENOMINATOR;
    netFee -= protocolShare;

    // Send protocol's share to the protocol fee receiver
    protocolFeeReceiver.safeTransferETH(protocolShare);

    // Send the remainder to the boost owner
    boost.owner.safeTransferETH(netFee);
}
```