Beautiful Wintergreen Gorilla

Medium

# Unlimited Referral Fee Allows Boost Creator to Bypass Protocol Fee

### Summary

The boost creator is allowed to set an unlimited referral fee. By setting the referral fee to 100%, the entire claim fee can be routed to a referrer, leaving no portion of the fee for the protocol. This allows a boost creator to bypass the intended fee-sharing mechanism, which is supposed to allocate a portion of the fees to the protocol.

### Root Cause

When users create  boost, they can set a  `referralFee` and a `protocolFee`

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/BoostCore.sol#L121C8-L122C23
```solidity
boost.protocolFee = protocolFee + payload_.protocolFee;
boost.referralFee = referralFee + payload_.referralFee;
```

When a user claims a boost, this function is call to handle fee

```solidity
    function _routeClaimFee(BoostLib.Boost storage boost, address referrer_) internal {
        if (claimFee == 0) return;
        uint256 netFee = claimFee;

        // If a referrer is provided, transfer the revshare and reduce the net fee
        if (referrer_ != address(0)) {
            uint256 referralShare = claimFee * boost.referralFee / FEE_DENOMINATOR;
            netFee -= referralShare;
            referrer_.safeTransferETH(referralShare);
        }

        // The remaining fee is split between the owner and the protocol
        boost.owner.safeTransferETH(netFee / 2);
        // owerFee = netFee / 2
        // protocoleFee = netFee - owerFee
        protocolFeeReceiver.safeTransferETH(address(this).balance);
    }
```

* The protocol does not impose a maximum cap on the `referralFee`. As a result, boost creators can manipulate the fee structure by setting the referral fee to 100%, effectively ensuring that the protocol receives nothing from the claim fee.
* The `boost.protocolFee` is not used in the process 


### Impact

If a boost creator sets the referral fee to 100%, the protocol will not receive any portion of the claim fee, which could result in significant loss of revenue over time.

### PoC

run in `test/BoostCore.t.sol`
```solidity
    function testClaimIncentive_WithReferrer_100() public {

        bytes memory validCreateData = LibZip.cdCompress(
        abi.encode(
            BoostCore.InitPayload({
                budget: budget,
                action: action,
                validator: BoostLib.Target({isBase: true, instance: address(0), parameters: ""}),
                allowList: allowList,
                incentives: _makeIncentives(1),
                protocolFee: 500, // 5% 
                referralFee: 9000, // referral fee to 90% + 10%
                maxParticipants: 10_000,
                owner: address(1)
            })
        ));
        
        boostCore.createBoost(validCreateData);

        // Mint an ERC721 token to the claimant (this contract)
        uint256 tokenId = 1;
        mockERC721.mint{value: 0.1 ether}(address(this));
        mockERC721.mint{value: 0.1 ether}(address(this));
        mockERC721.mint{value: 0.1 ether}(address(this));

        // Define a referrer
        address referrer = makeAddr("referrer");
        vm.deal(referrer, 1 ether); // Fund the referrer for testing purposes

        // Prepare the data payload for validation
        bytes memory data = abi.encode(address(this), abi.encode(tokenId));

        // Claim the incentive with a referrer
        boostCore.claimIncentive{value: 0.000075 ether}(0, 0, referrer, data);

        // Check the claims
        BoostLib.Boost memory boost = boostCore.getBoost(0);
        ERC20Incentive _incentive = ERC20Incentive(address(boost.incentives[0]));
        assertEq(_incentive.claims(), 1);

        // Check that the claim fee was routed to the referrer
        // the referrer get all the fee 0.000075 ether
        uint256 expectedReferrerBalance = 1 ether + 0.000075 ether ;
        assertEq(referrer.balance, expectedReferrerBalance);
    }
```

### Mitigation

Introduce a maximum limit (e.g., 50%) on the referral fee to ensure that a portion of the claim fee is always directed to the protocol.
