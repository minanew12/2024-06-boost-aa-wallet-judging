Attractive Mulberry Fly

Medium

# ManagedBudget doesn't comply with fee-on-transfer tokens

## Summary
Although it was stated in the audit README file that all weird tokens should be supported, as per:

> Protocol should work with all native and ERC20 tokens that adhear to standard including weird tokens.
No assumptions are made in regards to the properties of integrating tokens and weird trait tokens should be handled normally. Specifically we'd expect to interact with USDT and other stable coins/decimal 6 coins and erc20z from Zora as well as other fractional NFT contracts as long as they adhere to ERC20 standard. We aren't whitelisting tokens so any erc20 should work (including weird ones).

The fee-on-transfer tokens will not be transferable to the `ManagedBudget` contract, because there's a strict comparison between the `request.amount` and the current `ManagedBudget`'s balance.

***So the depositor will only have the ability to transfer tokens directly to the `ManagedBudget` contract, leaving the `allocate` function unusable for him. This is very bad for further external protocol integrations, as the `allocate` function should work normally, and will break integrations of other contracts with the `ManagedBudget` contract, if fee-on-transfer tokens are used as the `request.asset`.***

## Vulnerability Detail
```solidity
    function allocate(bytes calldata data_) external payable virtual override returns (bool) {
        Transfer memory request = abi.decode(data_, (Transfer));
        if (request.assetType == AssetType.ETH) {
            FungiblePayload memory payload = abi.decode(request.data, (FungiblePayload));

            // Ensure the value received is equal to the `payload.amount`
            if (msg.value != payload.amount) {
                revert InvalidAllocation(request.asset, payload.amount);
            }
        } else if (request.assetType == AssetType.ERC20) {
            FungiblePayload memory payload = abi.decode(request.data, (FungiblePayload));

            // Transfer `payload.amount` of the token to this contract
            request.asset.safeTransferFrom(request.target, address(this), payload.amount); 
            if (request.asset.balanceOf(address(this)) < payload.amount) { // HERE IS THE PROBLEM
                revert InvalidAllocation(request.asset, payload.amount);
            }
        } else if (request.assetType == AssetType.ERC1155) {
            // ...CLIP...
        }
}
```

## Impact
The protocol explicitly states that all https://github.com/d-xo/weird-erc20 tokens should be supported, as there's no whitelisting mechanism to ensure that end consumers will use standard tokens for scaffolding new boosts and budgets.

## Code Snippet
https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/d9f597776cc2d20fbb19ffb1f7731126cf3b6210/boost-protocol/packages/evm/contracts/budgets/ManagedBudget.sol#L71-L73

## Tool used
Manual review.

## Recommendation
You can either remove the after-transfer balance check completely, as:
```diff
    function allocate(bytes calldata data_) external payable virtual override returns (bool) {
        Transfer memory request = abi.decode(data_, (Transfer));
        if (request.assetType == AssetType.ETH) {
            FungiblePayload memory payload = abi.decode(request.data, (FungiblePayload));

            // Ensure the value received is equal to the `payload.amount`
            if (msg.value != payload.amount) {
                revert InvalidAllocation(request.asset, payload.amount);
            }
        } else if (request.assetType == AssetType.ERC20) {
            FungiblePayload memory payload = abi.decode(request.data, (FungiblePayload));

            // Transfer `payload.amount` of the token to this contract
            request.asset.safeTransferFrom(request.target, address(this), payload.amount);
-           if (request.asset.balanceOf(address(this)) < payload.amount) {
-               revert InvalidAllocation(request.asset, payload.amount);
-           }
        } else if (request.assetType == AssetType.ERC1155) {
            // ...CLIP...
        }
}
```

Or you can add an optional `minResidual` parameter like, by introducing an optional `minResidual` field in the `ABudget.sol` file:
```diff
    /// @notice The payload for an ETH or ERC20 transfer
    /// @param amount The amount of the asset to transfer
    struct FungiblePayload {
        uint256 amount;
+       uint256 minResidual; // OPTIONAL
    }
```

And updating the `ManagedBudget`'s `allocate` function in the following manner:
```diff
    function allocate(bytes calldata data_) external payable virtual override returns (bool) {
        Transfer memory request = abi.decode(data_, (Transfer));
        if (request.assetType == AssetType.ETH) {
            FungiblePayload memory payload = abi.decode(request.data, (FungiblePayload));

            // Ensure the value received is equal to the `payload.amount`
            if (msg.value != payload.amount) {
                revert InvalidAllocation(request.asset, payload.amount);
            }
        } else if (request.assetType == AssetType.ERC20) {
            FungiblePayload memory payload = abi.decode(request.data, (FungiblePayload));

            // Transfer `payload.amount` of the token to this contract
            request.asset.safeTransferFrom(request.target, address(this), payload.amount);
+           if (request.asset.balanceOf(address(this)) < payload.minResidual && payload.minResidual > 0 && payload.minResidual != type(uint256).max) {
+               revert InvalidAllocation(request.asset, payload.amount);
+           }
        } else if (request.assetType == AssetType.ERC1155) {
            // ...CLIP...
        }
}
```