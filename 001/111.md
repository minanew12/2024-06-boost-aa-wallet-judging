Attractive Mulberry Fly

Medium

# Any claimer can get back the {boost.referralFee %} of the claimFee they paid during claimIncentiveFor / claimIncentive

## Summary
Due to the fact that there's no validation of the `referrer` parameter, any user can continuously use `claimIncentiveFor` in the `BoostCore` to get `boost.referralFee` % of the `claimFee` that they have paid along with the message back, leaving the protocol with less profit than it was configured and expected.

## Vulnerability Detail
Any user can specify the `referrer` parameter as themselves and get part of the `claimFee` that they paid.

Although usually referral bonuses functionality is encouraged in protocol design, the referrer should be validated to be 1) not the `msg.sender`; 2) not the `claimant`; 3) and not just an arbitrary address.

## Impact
The referrer should be validated to be disctinct from the caller who pays the `claimFee` and the `claimant` who gets claimed on behalf of, and ideally a referrer should be validated to have reputation in the protocol.

***Most importantly, the `boost.owner` and the `protocolFeeReceiver` will end up with less fees than expected, leaving them slightly robbed.***

### Please note that `referralFee`s may be pretty high and are 100% customizeable.
> **Q: Are there any limitations on values set by admins (or other roles) in the codebase, including restrictions on array lengths?**
> BoostCore.sol::protocolFee = Between 0 - FEE_DENOMINATOR BoostCore.sol::referralFee = Between 0 - FEE_DENOMINATOR

As more users get to know about the pitfall, it will harm the fees that the protocol and the `boost.owner`s receive, because noone will want to pay the full `claimFee` amount, considering they have an ability to discount it infinitely.

## Code Snippet
Replace the `testClaimIncentive_WithReferrer` test partially in the `BoostCoreTest` to verify the problematic behavior:
```solidity
    function testClaimIncentive_WithReferrer() public {
        // Create a Boost first
        boostCore.createBoost(validCreateCalldata);

        // Mint an ERC721 token to the claimant (this contract)
        uint256 tokenId = 1;
        mockERC721.mint{value: 0.1 ether}(address(this));
        mockERC721.mint{value: 0.1 ether}(address(this));
        mockERC721.mint{value: 0.1 ether}(address(this));

        // Define a referrer
        address referrer = makeAddr("referrer");
        
        vm.startPrank(referrer);

        vm.deal(referrer, 1 ether); // Fund the referrer for testing purposes


        // Prepare the data payload for validation
        bytes memory data = abi.encode(address(this), abi.encode(tokenId));

        // Claim the incentive with a referrer
        boostCore.claimIncentive{value: 0.000075 ether}(0, 0, referrer, data); // assume it is paid by the "referrer", and the referrer is also the msg.sender too!

        // Check the claims
        BoostLib.Boost memory boost = boostCore.getBoost(0);
        ERC20Incentive _incentive = ERC20Incentive(address(boost.incentives[0]));
        assertEq(_incentive.claims(), 1);

        // Check that the claim fee was routed to the referrer
        uint256 expectedReferrerBalance = 1 ether + (0.000075 ether * 2000 / 10000);
        assertEq(referrer.balance, expectedReferrerBalance - 0.000075 ether); // the 0.000075 ether is the fee paid when calling claimIncentiveFor which is charged from the current vm.prank'er

        vm.stopPrank();
    }
```

You'll see the refund in the logs as well:
```solidity
    ├─ [159194] BoostCore::claimIncentive{value: 75000000000000}(0, 0, referrer: [0xbbD0F905B1ab8868f461976C520e518A623C0100], 0x0000000000000000000000007fa9385be102ac3eac297483dd6233d62b3e1496000000000000000000000000000000000000000000000000000000000000004000000000000000000000000000000000000000000000000000000000000000200000000000000000000000000000000000000000000000000000000000000001) 
    │   ├─ [0] referrer::fallback{value: 15000000000000}()
    │   │   └─ ← [Stop]
```

---

The problem is here:
```solidity
    /// @notice Claim an incentive for a Boost on behalf of another user
    /// @param boostId_ The ID of the Boost
    /// @param incentiveId_ The ID of the AIncentive
    /// @param referrer_ The address of the referrer (if any)
    /// @param data_ The data for the claim
    /// @param claimant the address of the user eligible for the incentive payout
    function claimIncentiveFor(
        uint256 boostId_,
        uint256 incentiveId_,
        address referrer_,
        bytes calldata data_,
        address claimant
    ) public payable nonReentrant {
        BoostLib.Boost storage boost = _boosts[boostId_];
        if (msg.value < claimFee) revert BoostError.InsufficientFunds(address(0), msg.value, claimFee);
        _routeClaimFee(boost, referrer_);

        // wake-disable-next-line reentrancy (false positive, function is nonReentrant)
        if (!boost.validator.validate(boostId_, incentiveId_, claimant, data_)) revert BoostError.Unauthorized();
        if (!boost.incentives[incentiveId_].claim(claimant, data_)) {
            revert BoostError.ClaimFailed(claimant, data_);
        }
        
    }

     // ...CLIP...

    /// @notice Route the claim fee to the creator, referrer, and protocol fee receiver
    /// @param boost The Boost for which to route the claim fee
    /// @param referrer_ The address of the referrer (if any)
    function _routeClaimFee(BoostLib.Boost storage boost, address referrer_) internal {
        if (claimFee == 0) return;
        uint256 netFee = claimFee;

        // If a referrer is provided, transfer the revshare and reduce the net fee
        if (referrer_ != address(0)) {
            uint256 referralShare = claimFee * boost.referralFee / FEE_DENOMINATOR;
            netFee -= referralShare;
            referrer_.safeTransferETH(referralShare);
        }

        // The remaining fee is split between the owner and the protocol
        boost.owner.safeTransferETH(netFee / 2);
        protocolFeeReceiver.safeTransferETH(address(this).balance);
    }
```

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/BoostCore.sol#L149
https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/BoostCore.sol#L222-L227
https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/BoostCore.sol#L310-L314


## Tool used
Manual review.

## Recommendation
Consider either removing the `referrer` option completely, or verifying the the `referrer` makes sense and has reputation, and not just a parameter to absuse the protocol.