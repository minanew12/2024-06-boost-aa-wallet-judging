Ancient Neon Koala

Medium

# Precision loss in reward for `CGDAIncentive`

## Summary
Tokens with a low amount of decimals will experience precision loss in `CGDAIncentive`. This violates the specification that the reward should increase by `rewardBoost` each hour and results in loss of funds for claimaints.

## Vulnerability Detail
The specifications of the protocol state that `Protocol should work with all native and ERC20 tokens that adhear to standard including weird tokens`. This includes tokens with low decimals. Some tokens like [Gemini USD](https://etherscan.io/token/0x056Fd409E1d7A124BD7017459dFEa2F387b6d5Cd?a=0x5f65f7b609678448494De4C87521CdF6cEf1e932#readContract) use only 2 decimals.

When calculating `currentReward` to be distributed to each claimant, the reward increases linearly over time in the absence of claims:

```solidiity
    function currentReward() public view override returns (uint256) {
        uint256 timeSinceLastClaim = block.timestamp - cgdaParams.lastClaimTime;
        uint256 available = asset.balanceOf(address(this));


        // Calculate the current reward based on the time elapsed since the last claim
        // on a linear scale, with `1 * rewardBoost` added for each hour without a claim
        uint256 projectedReward = cgdaParams.currentReward + (timeSinceLastClaim * cgdaParams.rewardBoost) / 3600;
        return projectedReward > available ? available : projectedReward;
    }
```

Note the specification `on a linear scale, with 1 * rewardBoost added for each hour without a claim`. For tokens with low precision, this will not be true if many claims are occuring within an hour, as the term `(timeSinceLastClaim * cgdaParams.rewardBoost) / 3600` will have some precision loss depending on the amounts and decimals.

## Impact
The specification is violated for tokens with low decimals as `1 * rewardBoost` is expected to be added on a linear scale over an hour. When there is a high frequency of claims in an hour, less than `1 * rewardBoost` will be added. 

In the PoC included below, GUSD was used and an error of 30% occured between the expected and observed boost in rewards over an hour. This spec is technically violated for every token but its more significant for tokens with low decimals. 

This will also cause some loss of funds for those claimants which had a lesser amount of boosted rewards than expected. 

## Code Snippet
https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/CGDAIncentive.sol#L123-L131

<details>
<summary>POC</summary>

// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.24;

import {Test, console} from "lib/forge-std/src/Test.sol";
import {MockERC20Decimals6} from "contracts/shared/Mocks.sol";

import {LibClone} from "@solady/utils/LibClone.sol";
import {SafeTransferLib} from "@solady/utils/SafeTransferLib.sol";

import {CGDAIncentive} from "contracts/incentives/CGDAIncentive.sol";

contract CGDAPrecisionTest is Test {
    using SafeTransferLib for address;

    MockERC20Decimals6 public asset = new MockERC20Decimals6();
    CGDAIncentive public incentive;

    // configuration parameters for this PoC
    uint256 initialReward = 100;
    uint256 rewardDecay = 1;
    uint256 rewardBoost = 10000;
    uint256 totalBudget = 100_000_000;

    function setUp() public {
        incentive = CGDAIncentive(LibClone.clone(address(new CGDAIncentive())));
        asset.mint(address(incentive), totalBudget); 
        incentive.initialize(
            abi.encode(
                CGDAIncentive.InitPayload({
                    asset: address(asset),
                    initialReward: initialReward, 
                    rewardDecay: rewardDecay, 
                    rewardBoost: rewardBoost, 
                    totalBudget: totalBudget
                })
            )
        );
    }

    function test_currentReward_WithClaims() public {
        address alice = makeAddr("alice unchained");
        uint256 decayed = 0;

        // No claims for 1 hours, reward should increase by rewardBoost
        skip(1 hours);
        assertEq(incentive.currentReward(), initialReward + rewardBoost);
        uint256 lastCheckedReward = initialReward + rewardBoost;

        _makeClaim(alice);

        // Claims every second for the next hour
        for(uint i = 0; i < 3600; i++) {
            skip(1 seconds);

            _makeClaim(alice);
            decayed += rewardDecay; // tracking how much reward is decaying between claims
        }

        // normalize out the decay between claims.
        uint256 lastCheckedRewardLessFee = lastCheckedReward - decayed;

        // reward should still increase by rewardBoost in an hour (adjusted for decay)
        assertEq(incentive.currentReward() - lastCheckedRewardLessFee, rewardBoost);
    }

    function _makeClaim(address target_) internal {
        incentive.claim(target_, hex"");
    }
}
</details>

## Tool used

Manual Review

## Recommendation
When the token decimals are low, use a higher decimal internal representation of the current reward. When transferring to the user, it is acceptable to truncate it as the precision loss will not accrue globally.