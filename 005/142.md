Overt Vermilion Mallard

High

# Incentive data can be used on the wrong incentive contract

### Summary

Incentive data can be used on the wrong incentive contract

### Root Cause

A key part of the design is that only one signature is required to unlock all incentives, as we can see the `incentiveId` is not present in the hash the validator signs. Instead the `incentiveQuantity` is signed, which represents the number of incentives in the boost.
```solidity
    /// Validate that the action has been completed successfully by constructing a payload and checking the signature against it
    /// @inheritdoc AValidator
    function validate(uint256 boostId, uint256 incentiveId, address claimant, bytes calldata claimData)
        external
        override
        returns (bool)
    {
        if (msg.sender != _validatorCaller) revert BoostError.Unauthorized();

        (BoostClaimData memory claim) = abi.decode(claimData, (BoostClaimData));
        (SignerValidatorInputParams memory validatorData) =
            abi.decode(claim.validatorData, (SignerValidatorInputParams));

        bytes32 hash = hashSignerData(boostId, validatorData.incentiveQuantity, claimant, claim.incentiveData);

        if (uint256(validatorData.incentiveQuantity) <= incentiveId) {
            revert BoostError.InvalidIncentive(validatorData.incentiveQuantity, incentiveId);
        }
        if (!signers[validatorData.signer]) revert BoostError.Unauthorized();

        // Mark the incentive as claimed to prevent replays
        // checks internally if the incentive has already been claimed
        _used.setOrThrow(hash, incentiveId);

        // Return the result of the signature check
        // no need for a sig prefix since it's encoded by the EIP712 lib
        return validatorData.signer.isValidSignatureNow(hash, validatorData.signature);
    }
```


The exception is if multiple incentive contracts require multiple `incentiveData`. However in the incentive contracts, there is no validation that the incentive data is meant for the particular contract.

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20VariableIncentive.sol#L62-L82
```solidity
    /// @notice Claim the incentive with variable rewards
    /// @param data_ The data payload for the incentive claim `(uint256signedAmount)`
    /// @return True if the incentive was successfully claimed
    function claim(address claimTarget, bytes calldata data_) external override onlyOwner returns (bool) {
        BoostClaimData memory boostClaimData = abi.decode(data_, (BoostClaimData));
        uint256 signedAmount = abi.decode(boostClaimData.incentiveData, (uint256));
        uint256 claimAmount;
        if (!_isClaimable(claimTarget)) revert NotClaimable();

        if (reward == 0) {
            claimAmount = signedAmount;
        } else {
            // NOTE: this is assuming that the signed scalar is in ETH decimal format
            claimAmount = reward * signedAmount / 1e18;
        }

        if (totalClaimed + claimAmount > limit) revert ClaimFailed();

        totalClaimed += claimAmount;
        asset.safeTransfer(claimTarget, claimAmount);

        emit Claimed(claimTarget, abi.encodePacked(asset, claimTarget, claimAmount));
        return true;
    }
```

### Internal pre-conditions

Boost with multiple incentive contracts which require different incentive data.

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Consider 2 ERC20VariableIncentive contracts. Suppose the `signedAmount` for contract 1 is 1e18 and the `signedAmount` for contract 2 is 100e18. An attacker obtains the 2 different validator signatures for the 2 `incentiveData` required. However, the signature for contract 2, can be easily be reused on contract 1, thereby allowing unauthorized withdrawing 100e18 tokens from contract 1. Similarly, the signature for contract 1 can also be used on contract 2, thereby allowing unauthorized withdrawing 1e18 tokens from contract 2.

The same also works for a boost that consists of 1 ERC20VariableIncentive contract and 1 ERC1155Incentive contract, the `incentiveData` meant for the ERC1155Incentive contract can be used on the `ERC20VariableIncentive` contract.

### PoC

_No response_

### Mitigation

Include the address of the incentive contract in the incentive data.

Then in the incentive contract, validate that the address passed is equal to `address(this)`.