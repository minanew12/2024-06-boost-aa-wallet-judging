Festive Clay Bobcat

High

# Missing `claimed` flag update in `claim` function allows multiple claims

### Summary

The missing flag update in the [claim](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/CGDAIncentive.sol#L85) function will cause multiple rewards claims

### Root Cause

In `claim` function in `CGDAIncentive.sol` there is a missing flag update on the `claimed[claimTarget]` after a successful claim.[](url)

```solidity
/// @inheritdoc AIncentive
    /// @notice Claim the incentive
    function claim(address claimTarget, bytes calldata) external virtual override onlyOwner returns (bool) {
        if (!_isClaimable(claimTarget)) revert NotClaimable();

         /// @audit missing claimed[claimTarget] = true;
        claims++;


        // Calculate the current reward and update the state
        uint256 reward = currentReward();
        cgdaParams.lastClaimTime = block.timestamp;
        cgdaParams.currentReward =
            reward > cgdaParams.rewardDecay ? reward - cgdaParams.rewardDecay : cgdaParams.rewardDecay;

        // Transfer the reward to the recipient
        asset.safeTransfer(claimTarget, reward);

        emit Claimed(claimTarget, abi.encodePacked(asset, claimTarget, reward));
        return true;
    }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

The check in `claim` function `if (!_isClaimable(claimTarget)) revert NotClaimable();` is checking does the mapping `claimed` with the `recipient_` address is set to true:
```solidity
function _isClaimable(address recipient_) internal view returns (bool) {
        uint256 reward = currentReward();
        return reward > 0 && asset.balanceOf(address(this)) >= reward && !claimed[recipient_];
    }
```
But in `claim` function the claimed mapping it isnt set to true after claim. This missed state change can lead to multiple claim from the same user

### Impact

The user can claim the reward multiple times

### PoC

```solidity
function test_MultipleClaims() public {
        // Ensure the current reward is initially set to 1 ether
        assertEq(incentive.currentReward(), 1 ether);

        // Use a single account for testing
        address recipient = makeAddr("test recipient");

        //  First claim should succeed
        bool hasClaimedBeforeFirst = incentive.claimed(recipient);
        assertFalse(hasClaimedBeforeFirst, "Recipient should not have claimed before");

        // Call the claim function for the first time
        incentive.claim(recipient, hex"");

        // After the first claim, the recipient should have claimed
        bool hasClaimedAfterFirst = incentive.claimed(recipient);
        assertTrue(hasClaimedAfterFirst, "Recipient should have claimed after first claim");

        // Attempt to claim a second time
        vm.expectRevert("NotClaimable"); // Expect revert with "NotClaimable" message
        incentive.claim(recipient, hex"");
    }

[FAIL. Reason: Recipient should have claimed after first claim]

```

### Mitigation

Ensure the `claimed[claimTarget]` flag is updated to `true` immediately after a successful claim. This will prevent further claims by the same recipient and secure the contract against multiple claims by the same staker.