Melted Pewter Owl

High

# In `CGDAIncentive::claim` and `ERC20VariableIncentive::claim` users who have already claimed the incentive once can keep claiming it again and again until the `limit` is reached.

## Summary
In `CGDAIncentive::claim` and `ERC20VariableIncentive::claim` users who have already claimed the incentive once can keep claiming it again and again until the `limit` is reached. This is due to not updating the mapping `claimed` in those above mentioned functions.

## Vulnerability Detail

If userA(Attacker), has performed a valid "action" for the first time then in order to claim his incentives he will execute `function claimIncentiveFor` on `BoostCore.sol`, and he will get his incentive(reward) transferred from Incentive contract to his account.

The real EXPLOIT is here:
The same userA came again and performed the valid "action" again and then executed `function claimIncentiveFor` in order to claim the rewards again but this time the user changes the `bytes incentiveData` which is denoted in `struct BoostClaimData` and passed in encoded `bytes calldata data_` while calling `BoostCore::claimIncentiveFor` 

```solidity
  
    function claimIncentiveFor(
        uint256 boostId_,
        uint256 incentiveId_,
        address referrer_,
@>        bytes calldata data_,
        address claimant
    ) public payable nonReentrant {
        BoostLib.Boost storage boost = _boosts[boostId_];
        if (msg.value < claimFee) revert BoostError.InsufficientFunds(address(0), msg.value, claimFee);
        _routeClaimFee(boost, referrer_);

        // wake-disable-next-line reentrancy (false positive, function is nonReentrant)
@>        if (!boost.validator.validate(boostId_, incentiveId_, claimant, data_)) revert BoostError.Unauthorized();
@>        if (!boost.incentives[incentiveId_].claim(claimant, data_)) {
            revert BoostError.ClaimFailed(claimant, data_);
        }
    }


```

Now inside the `function claimIncentiveFor` this `boost.validator.validate()` function will execute:

```solidity
    function validate(uint256 boostId, uint256 incentiveId, address claimant, bytes calldata claimData)
        external
        override
        returns (bool)
    {
        if (msg.sender != _validatorCaller) revert BoostError.Unauthorized();

        (BoostClaimData memory claim) = abi.decode(claimData, (BoostClaimData));
        (SignerValidatorInputParams memory validatorData) =
            abi.decode(claim.validatorData, (SignerValidatorInputParams));

@>        bytes32 hash = hashSignerData(boostId, validatorData.incentiveQuantity, claimant, claim.incentiveData);

        if (uint256(validatorData.incentiveQuantity) <= incentiveId) {
            revert BoostError.InvalidIncentive(validatorData.incentiveQuantity, incentiveId);
        }
        if (!signers[validatorData.signer]) revert BoostError.Unauthorized();

        // Mark the incentive as claimed to prevent replays
        // checks internally if the incentive has already been claimed
@>        _used.setOrThrow(hash, incentiveId);

        // Return the result of the signature check
        // no need for a sig prefix since it's encoded by the EIP712 lib
        return validatorData.signer.isValidSignatureNow(hash, validatorData.signature);
    }
```

here this function due to this line check - `_used.setOrThrow(hash, incentiveId);` this tx would have reverted in this situation because the same userA is trying to `claim` again(which we do not want) 

but here because the userA has changed the `incentiveData`, the calculated `bytes32 hash = hashSignerData(boostId, validatorData.incentiveQuantity, claimant, claim.incentiveData);` will be different than the one that was generated in his first time claim, therefore the line - ``_used.setOrThrow(hash, incentiveId);`` will not revert.

And later the line to claim the incentive on Incentive contract will be executed by `BoostCore::claimIncentiveFor` - 
`boost.incentives[incentiveId_].claim()`
And this function will also be executed successfully. Letting the userA claim again multiple time until limit has reached. 


This vulnerability can only happen on 2 incentive contracts named: `CGDAIncentive.sol` and `ERC20VariableIncentive.sol` because they do not update the `claimed` mapping in their `function claim` when a user claims the reward for the first time.

In both of the contract's `claim function` there is no updated of mapping like this - `claimed[claimTarget] = true` which updates the user has already 

In `CGDAIncentive::claim`:
```solidity
    function claim(address claimTarget, bytes calldata) external virtual override onlyOwner returns (bool) {
        if (!_isClaimable(claimTarget)) revert NotClaimable();
        claims++;

        // Calculate the current reward and update the state
        uint256 reward = currentReward();
        cgdaParams.lastClaimTime = block.timestamp;
        cgdaParams.currentReward =
            reward > cgdaParams.rewardDecay ? reward - cgdaParams.rewardDecay : cgdaParams.rewardDecay;

        // Transfer the reward to the recipient
        asset.safeTransfer(claimTarget, reward);

        emit Claimed(claimTarget, abi.encodePacked(asset, claimTarget, reward));
        return true;
    }
```

 In `ERC20VariableIncentive::claim`:
 
```solidity
    function claim(address claimTarget, bytes calldata data_) external override onlyOwner returns (bool) {
        BoostClaimData memory boostClaimData = abi.decode(data_, (BoostClaimData));
        uint256 signedAmount = abi.decode(boostClaimData.incentiveData, (uint256));
        uint256 claimAmount;
        if (!_isClaimable(claimTarget)) revert NotClaimable();

        if (reward == 0) {
            claimAmount = signedAmount;
        } else {
            // NOTE: this is assuming that the signed scalar is in ETH decimal format
            claimAmount = reward * signedAmount / 1e18;
        }

        if (totalClaimed + claimAmount > limit) revert ClaimFailed();

        totalClaimed += claimAmount;
        asset.safeTransfer(claimTarget, claimAmount);

        emit Claimed(claimTarget, abi.encodePacked(asset, claimTarget, claimAmount));
        return true;
    }
```


## Impact
users who have already claimed the incentive once will keep claiming it again and again until the limit is reached. This will hinder honest users as their maximum rewards will be claimed by the same userA(attacker) again and again.
## Code Snippet
https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/incentives/CGDAIncentive.sol#L85

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/incentives/ERC20VariableIncentive.sol#L62

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/BoostCore.sol#L176
## Tool used

Manual Review

## Recommendation
Add this mapping update - `claimed[claimTarget] = true` in both the function.

By doing so, no user will be able to claim again.