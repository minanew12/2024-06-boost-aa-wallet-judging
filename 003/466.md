Immense Cream Trout

High

# `claimed[claimTarget]` not recorded in `CGDAIncentive::claim`

## Summary

The CGDAIncentive contract contains a critical vulnerability in its `claim` function. The function fails to update the `claimed` mapping for users who have successfully claimed rewards, potentially allowing multiple claims by the same user.

## Vulnerability Detail

The vulnerability is located in the `claim` function of the CGDAIncentive contract. After a successful claim, the function updates various state variables but neglects to set `claimed[claimTarget]` to `true` as show below:

```solidity
function claim(address claimTarget, bytes calldata) external virtual override onlyOwner returns (bool) {
    if (!_isClaimable(claimTarget)) revert NotClaimable();
    claims++;

    //@audit claimed[claimTarget] not set

    // Calculate the current reward and update the state
    uint256 reward = currentReward();
    cgdaParams.lastClaimTime = block.timestamp;
    cgdaParams.currentReward =
        reward > cgdaParams.rewardDecay ? reward - cgdaParams.rewardDecay : cgdaParams.rewardDecay;

    // Transfer the reward to the recipient
    asset.safeTransfer(claimTarget, reward);

    emit Claimed(claimTarget, abi.encodePacked(asset, claimTarget, reward));
    return true;
}
```

This omission can lead to multiple claims by the same user as the `_isClaimable` call will always return false (until all rewards are depleted), and especially if the external validation method used (performed by the validator contract beforehand) does not record user claims and assumes the incentive contract will do so.

```solidity
function _isClaimable(address recipient_) internal view returns (bool) {
    uint256 reward = currentReward();
    return reward > 0 && asset.balanceOf(address(this)) >= reward && !claimed[recipient_];
}
```

Here's a breakdown of the issue:

1. The `claim` function checks if the claim is valid using `_isClaimable(claimTarget)`.
2. If valid, it processes the claim, updates CGDA parameters, and transfers the reward.
3. However, it fails to update `claimed[claimTarget]` to prevent future claims by the same user.

**Example scenario:**
- User A claims a reward successfully.
- The `claimed[User A]` remains `false`.
- User A can claim again in the next transaction, as long as the external validation doesn't prevent it.

This vulnerability could allow a user to claim rewards multiple times, depleting the contract's funds more quickly than intended and disrupting the CGDA mechanism.

This is a valid concern because boost creators may assume that the CGDAIncentive contract handles claim recording, which would eliminate the need for an additional recording step in the validator contract they use or deploy. 

However, the protocol does not require boost creators to use only the SignerValidator (which has built-in claim recording) or to implement claim recording in their custom validator. 

As a result, boost creators may mistakenly believe that claim recording is already managed by CGDAIncentive, leading to the potential loss of all incentive funds if claims are not properly tracked.

## Impact

users could claim multiple times and drain the whole incentive budget leading to financial loss for the boost creator (who did fund the incentive) and other boost participants

## Code Snippet

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/CGDAIncentive.sol#L85-L100

## Tool used

Manual Review

## Recommendation

To fix this vulnerability, the `claim` function should be modified to update the `claimed` mapping after a successful claim. Here's the recommended implementation:

```solidity
function claim(address claimTarget, bytes calldata) external virtual override onlyOwner returns (bool) {
    if (!_isClaimable(claimTarget)) revert NotClaimable();
    claims++;
    // Update the claimed mapping to prevent multiple claims
    claimed[claimTarget] = true;

    // Calculate the current reward and update the state
    uint256 reward = currentReward();
    cgdaParams.lastClaimTime = block.timestamp;
    cgdaParams.currentReward =
        reward > cgdaParams.rewardDecay ? reward - cgdaParams.rewardDecay : cgdaParams.rewardDecay;

    // Transfer the reward to the recipient
    asset.safeTransfer(claimTarget, reward);

    emit Claimed(claimTarget, abi.encodePacked(asset, claimTarget, reward));
    return true;
}
```