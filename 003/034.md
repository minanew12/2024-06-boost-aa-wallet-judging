Gigantic Alabaster Yak

Medium

# CGDAIncentive.sol :: claim() the same recipient can claim the reward multiple times.

### Summary

**`claim()`** is used to claim rewards for a specific target. The issue is that the **`claimed`** mapping is not updated, allowing the same recipient to claim the reward multiple times.

### Root Cause

[claim()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/d9f597776cc2d20fbb19ffb1f7731126cf3b6210/boost-protocol/packages/evm/contracts/incentives/CGDAIncentive.sol#L85-L100) is implemented as follows:
```Solidity
function claim(address claimTarget, bytes calldata) external virtual override onlyOwner returns (bool) {
        if (!_isClaimable(claimTarget)) revert NotClaimable();
        claims++;

        // Calculate the current reward and update the state
        uint256 reward = currentReward();
        cgdaParams.lastClaimTime = block.timestamp;
        cgdaParams.currentReward =
            reward > cgdaParams.rewardDecay ? reward - cgdaParams.rewardDecay : cgdaParams.rewardDecay;

        // Transfer the reward to the recipient
        asset.safeTransfer(claimTarget, reward);

        emit Claimed(claimTarget, abi.encodePacked(asset, claimTarget, reward));
        return true;
    }
```

As you can see, it checks whether the **`claimTarget`** is eligible to claim the reward using [_isClaimable](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/d9f597776cc2d20fbb19ffb1f7731126cf3b6210/boost-protocol/packages/evm/contracts/incentives/CGDAIncentive.sol#L133-L136).

```Solidity
function _isClaimable(address recipient_) internal view returns (bool) {
        uint256 reward = currentReward();
        return reward > 0 && asset.balanceOf(address(this)) >= reward && !claimed[recipient_];
    }
}
```
As you can see, to claim the reward, the recipient must not have claimed it before. 

However, there is no point in the code where the **`claimed`** mapping is set to true for the current recipient, allowing them to claim the reward multiple times.

### Internal pre-conditions

The owner must claim the reward using the same address.

### External pre-conditions

None.

### Attack Path

None.

### Impact

The same recipient can claim the reward multiple times, allowing them to receive more rewards than intended, which prevents others from claiming their rewards.

### PoC

To demonstrate the issue, copy the following POC into **`CGDAIncentive.t.sol`**.
```Solidity
function test_sameAddressClaimAllRewards() public {
        assertEq(incentive.currentReward(), 1 ether);

        address[] memory accounts = _randomAccounts(16);
        for (uint256 i = 0; i < accounts.length; i++) {
            //claim using the same address
            incentive.claim(accounts[0], hex"");
        }

        assertEq(incentive.currentReward(), 0 ether);
        assertEq(asset.balanceOf(address(incentive)), 0 ether);
    }
```

### Mitigation

To resolve the issue, update the **`claimed`** mapping.
```diff
function claim(address claimTarget, bytes calldata) external virtual override onlyOwner returns (bool) {
        if (!_isClaimable(claimTarget)) revert NotClaimable();
        claims++;

        // Calculate the current reward and update the state
        uint256 reward = currentReward();
        cgdaParams.lastClaimTime = block.timestamp;
        cgdaParams.currentReward =
            reward > cgdaParams.rewardDecay ? reward - cgdaParams.rewardDecay : cgdaParams.rewardDecay;
        
+       claimed[claimTarget] = true;

        // Transfer the reward to the recipient
        asset.safeTransfer(claimTarget, reward);

        emit Claimed(claimTarget, abi.encodePacked(asset, claimTarget, reward));
        return true;
    }
```