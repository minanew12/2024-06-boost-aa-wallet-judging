Itchy Mulberry Mandrill

Medium

# Revert of SignerValidator.validate() caused by bug in IncentiveBits.setOrThrow()

## Summary

The IncentiveBits.setOrThrow() function is used to mark certain combinations of hash and incentiveId as used to prevent replay attacks. The problem is that due to a bug in the implementation of setOrThrow(), in certain situations it will revert, which in turn causes SignerValidator.validate() and BoostCore.claimIncentiveFor to revert, preventing users from claiming their incentives.

## Vulnerability Detail

```solidity
function setOrThrow(IncentiveMap storage bitmap, bytes32 hash, uint256 incentive) internal {
    bytes4 invalidSelector = BoostError.IncentiveToBig.selector;
    bytes4 claimedSelector = BoostError.IncentiveClaimed.selector;
    /// @solidity memory-safe-assembly
    assembly {
        if gt(incentive, 7) {
            // if the incentive is larger the 7 (the highest bit index)
            // we revert
            mstore(0, invalidSelector)
            mstore(4, incentive)
            revert(0x00, 0x24)
        }
        mstore(0x20, bitmap.slot)
        mstore(0x00, hash)
        let storageSlot := keccak256(0x00, 0x40)
        // toggle the value that was stored inline on stack with xor
        let updatedStorageValue := xor(sload(storageSlot), shl(incentive, 1))
        // isolate the toggled bit and see if it's been unset back to zero
        let alreadySet := xor(1, shr(incentive, updatedStorageValue))
        if alreadySet {
            // revert if the stored value was unset
            mstore(0, claimedSelector)
            mstore(4, incentive)
            revert(0x00, 0x24)
        }
        // otherwise store the newly set value
        sstore(storageSlot, updatedStorageValue)
    }
```
https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/validators/SignerValidator.sol#L126-L153

The setOrThrow function stores an 8-bit number for each hash, with each bit corresponding to an incentiveId in the range [0,7]. After the corresponding storage slot is loaded, an XOR operation is performed between the current value and shl(incentiveId, 1). If incentiveId is 1, the value of shl(incentiveId, 1) in binary is 10, which means that in updatedStorageValue, bit 1 will be 0 if the corresponding bit in storage is 1 and 1 otherwise. On the next line of code, an XOR operation is performed between 1 and shr(incentive, updatedStorageValue). The goal of shr(incentive, updatedStorageValue) is to shift right the value of the bit from position incentiveId to position 0 so that, after XORing with 1, it can check whether the bit was already set. If it was, the value of alreadySet should be 1; otherwise, it should be 0. The problem is that this code does not take into account the possibility of having non-zero bits at positions greater than incentiveId. For example:

1) A user calls claim with parameters hash = "hash1" and incentiveId = 2, which makes the value of hash1 in the bitmap equal to 00000100.

2) The user calls claim with parameters hash = "hash1" and incentiveId = 1. According to the algorithm described above, updatedStorageValue = 00000110, and alreadySet = xor(1, 0000011) = 0000010, which is not equal to 0. Therefore, setOrThrow will revert.

For this to happen, there need to be at least 2 incentiveIds for which the value of hash matches, and the claim function is called first for the incentive with the larger incentiveId. The value of hash is obtained by calling hashSignerData with parameters (boostId, validatorData.incentiveQuantity, claimant, claim.incentiveData), which shows that it is entirely possible to have matching hashes, for example, when more than one incentive contract is created accepted identical parameters, such as those originating from the same base.

<details>
<summary>POC</summary>

```solidity
  function testValidate_ValidSignature() public {
      uint256 boostId = 5;
      uint256 incentiveId = 2;
      uint8 incentiveQuantity = 3;
      address claimant = makeAddr("claimant");
      bytes memory incentiveData = hex"def456232173821931823712381232131391321934";
      bytes32 msgHash = validator.hashSignerData(boostId, incentiveQuantity, claimant, incentiveData);
      bytes memory signature = _signHash(msgHash, testSignerKey);

      ASignerValidator.SignerValidatorInputParams memory validatorData =
          ASignerValidator.SignerValidatorInputParams(testSigner, signature, incentiveQuantity);
      bytes memory claimData = abi.encode(IBoostClaim.BoostClaimData(abi.encode(validatorData), incentiveData));
      
      
      assertTrue(validator.validate(boostId, incentiveId, claimant, claimData));
      assertTrue(validator.validate(boostId, 1, claimant, claimData)); // this will revert

  }
```

</details>

## Impact

Broken core functionality and inability for the users to claim their tokens.

## Code Snippet

## Tool used

Manual Review

## Recommendation

Fix setOrThrow() to isolate the corresponding bit correctly.