Formal Charcoal Albatross

Medium

# by leveraging Uninitialized `createBoostAuth` Allows `Unauthorized` Boost Creation

## Summary

## Vulnerability Detail

The `createBoost` function in the `BoostCore` contract is expected to be called by a `trusted` creator, which is verified using the `createBoostAuth.isAuthorized(caller)` check. However, the `createBoostAuth` is not `initialized` in the `constructor`. By leveraging this anyone can front-run the `setCreateBoostAuth` call and invoke the `createBoost` function and create a malicious boost, bypassing the `canCreateBoost` modifier check.

```solidity
function createBoost(bytes calldata data_)
        external
        canCreateBoost(msg.sender)
        nonReentrant
        returns (BoostLib.Boost memory)
    {
        InitPayload memory payload_ = abi.decode(data_.cdDecompress(), (InitPayload));

        // Validate the Budget
        _checkBudget(payload_.budget);

        // Initialize the Boost
        BoostLib.Boost storage boost = _boosts.push();
        boost.owner = payload_.owner;
        boost.budget = payload_.budget;
        boost.protocolFee = protocolFee + payload_.protocolFee;
        boost.referralFee = referralFee + payload_.referralFee;
        boost.maxParticipants = payload_.maxParticipants;

        // Setup the Boost components
        boost.action = AAction(_makeTarget(type(AAction).interfaceId, payload_.action, true));
        boost.allowList = AAllowList(_makeTarget(type(AAllowList).interfaceId, payload_.allowList, true));
        boost.incentives = _makeIncentives(payload_.incentives, payload_.budget);
        boost.validator = AValidator(
            payload_.validator.instance == address(0)
                ? boost.action.supportsInterface(type(AValidator).interfaceId) ? address(boost.action) : address(0)
                : _makeTarget(type(AValidator).interfaceId, payload_.validator, true)
        );
        emit BoostCreated(
            _boosts.length - 1,
            boost.owner,
            address(boost.action),
            boost.incentives.length,
            address(boost.validator),
            address(boost.allowList),
            address(boost.budget)
        );
        return boost;
    }
```
```solidity
modifier canCreateBoost(address sender) {
        if (address(createBoostAuth) != address(0) && !createBoostAuth.isAuthorized(sender)) {
            revert BoostError.Unauthorized();
        }
        _;
    }
```
as we can see above if the `createBoostAuth` is not set ,then the enitre check passes

### **Exploit Scenario**

1. **Deployment**: The `BoostCore` contract is deployed without initializing the `createBoostAuth` contract in the constructor.
2. **Front-Running**: Before the owner calls `setCreateBoostAuth`, an attacker can front-run this transaction.
3. **Unauthorized Boost Creation**: The attacker calls the `createBoost` function. Since the `createBoostAuth` is not set, the `canCreateBoost` modifier check is bypassed.
4. **Malicious Boost**: The attacker successfully creates a malicious boost by bypassing the `canCreateBoost` check.




## Impact

By leveraging this exploit, anyone can create a malicious boost by bypassing the canCreateBoost check, which is intended to restrict boost creation to authorized addresses only.

## Code Snippet

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/BoostCore.sol#L75C5-L80C6

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/BoostCore.sol#L106

## Tool used

Manual Review

## Recommendation
* Initialize the createBoostAuth contract in the constructor to ensure that the canCreateBoost check is enforced from the moment the contract is deployed. This will prevent unauthorized boost creation before the setCreateBoostAuth function is called.

```solidity
constructor(BoostRegistry registry_, address protocolFeeReceiver_, address createBoostAuth_) {
    _initializeOwner(msg.sender);
    registry = registry_;
    protocolFeeReceiver = protocolFeeReceiver_;
    createBoostAuth = IAuth(createBoostAuth_);
}

```