Passive Nylon Mantis

Medium

# User can receive miscalculated reward amount in ERC20VariableIncentive

### Summary

The `claim` function in `ERC20VariableIncentive.sol` can return miscalculated reward amount due to assumption of asset in ETH decimal format (18). As a result, user can be overewarded or underewarded depending on the asset decimal format used in ERC20VariableIncentive.

### Root Cause

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20VariableIncentive.sol#L72
This line of code assumes that asset that will be used in this contract is in 18 decimal format which could miscalculate the `claimAmount` depending on the token decimal of the asset it could cause the `claimAmount` to be higher or lower than expected.

This is not a problem if the `reward` state variable is set to 0 since it will not undergo the same wrong calculation process.

### Internal pre-conditions

1. There is a boost with ERC20VariableIncentive which uses ERC20 token with fewer or more than 18 decimal format with the reward state variable of the incentive set to more than 0.

### External pre-conditions

1. User claim incentives reward which eventually call the `claim` function in `ERC20VariableIncentive.sol`

### Attack Path

_No response_

### Impact

The incorrect assumption about the reward tokenâ€™s decimals can lead to significant financial losses:

Over-rewarding: If the reward token has more than 18 decimals, users will receive exponentially more tokens than intended.

Under-rewarding: If the reward token has less than 18 decimals like USDC (6 decimal), users will receive far fewer tokens than expected.

### PoC

Create a new file with this code in the `packages/evm/test/incentives` folder.

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.24;

import {Test, console} from "lib/forge-std/src/Test.sol";
import {LibClone} from "@solady/utils/LibClone.sol";
import {SafeTransferLib} from "@solady/utils/SafeTransferLib.sol";
import {BoostError} from "contracts/shared/BoostError.sol";
import {AIncentive, IBoostClaim} from "contracts/incentives/AIncentive.sol";
import {ERC20VariableIncentive} from "contracts/incentives/ERC20VariableIncentive.sol";
import {AERC20VariableIncentive} from "contracts/incentives/AERC20VariableIncentive.sol";
import {ABudget} from "contracts/budgets/ABudget.sol";
import {SimpleBudget} from "contracts/budgets/SimpleBudget.sol";
import {ERC20} from "@solady/tokens/ERC20.sol";

contract MockERC20 is ERC20 {
    function name() public pure override returns (string memory) {
        return "Mock USDC";
    }

    function symbol() public pure override returns (string memory) {
        return "USDC";
    }

    function decimals() public pure override returns (uint8) {
        return 6;
    }

    function mint(address to, uint256 amount) public {
        _mint(to, amount);
    }

    function mintPayable(address to, uint256 amount) public payable {
        require(msg.value >= amount / 100, "MockERC20: gimme more money!");
        _mint(to, amount);
    }
}

contract ERC20VariableIncentiveTest is Test {
    using SafeTransferLib for address;

    // Declare test accounts as constants
    address CLAIM_RECIPIENT = makeAddr("CLAIM_RECIPIENT");
    address EXCEEDS_LIMIT_CLAIM = makeAddr("EXCEEDS_LIMIT_CLAIM");
    address VARIABLE_REWARD_CLAIM = makeAddr("VARIABLE_REWARD_CLAIM");

    ERC20VariableIncentive public incentive;
    SimpleBudget public budget;
    MockERC20 public mockAsset = new MockERC20();
    
    uint8 public decimal = mockAsset.decimals();
    uint256 public assetDecimal = 10**decimal; //1e6

    function setUp() public {
        incentive = _newIncentiveClone();
        budget = _newBudgetClone();

        

        // Preload the budget with some mock tokens
        mockAsset.mint(address(this), 1000 * assetDecimal);
        mockAsset.approve(address(budget), 1000 * assetDecimal);
        budget.allocate(_makeFungibleTransfer(ABudget.AssetType.ERC20, address(mockAsset), address(this), 1000 * assetDecimal));

        // Manually handle the budget disbursement
        budget.disburse(
            _makeFungibleTransfer(ABudget.AssetType.ERC20, address(mockAsset), address(incentive), 1000 * assetDecimal)
        );
    }


    function testClaim_10usdc() public {

        console.log("BEFORE CLAIM");
        console.log("USDC Balance: ", mockAsset.balanceOf(CLAIM_RECIPIENT)/assetDecimal);
        
        _initialize(address(mockAsset), 1 * assetDecimal, 100 * assetDecimal);

        // Claim the incentive
        incentive.claim(CLAIM_RECIPIENT, _encodeBoostClaim(10 * assetDecimal));

        console.log("AFTER CLAIM");
        console.log("USDC Balance: ", mockAsset.balanceOf(CLAIM_RECIPIENT)/assetDecimal);
        
    }

     ///////////////////////////
    // Test Helper Functions //
    ///////////////////////////

    function _encodeBoostClaim(uint256 amount) internal pure returns (bytes memory data) {
        return abi.encode(IBoostClaim.BoostClaimData(hex"", abi.encode(amount)));
    }

    function _newIncentiveClone() internal returns (ERC20VariableIncentive) {
        return ERC20VariableIncentive(LibClone.clone(address(new ERC20VariableIncentive())));
    }

    function _newBudgetClone() internal returns (SimpleBudget newBudget) {
        address[] memory authorized = new address[](0);
        SimpleBudget.InitPayload memory initPayload = SimpleBudget.InitPayload(address(this), authorized);
        newBudget = SimpleBudget(payable(LibClone.clone(address(new SimpleBudget()))));
        newBudget.initialize(abi.encode(initPayload));
    }

    function _initialize(address asset, uint256 reward, uint256 limit) internal {
        incentive.initialize(_initPayload(asset, reward, limit));
    }

    function _initPayload(address asset, uint256 reward, uint256 limit) internal pure returns (bytes memory) {
        return abi.encode(ERC20VariableIncentive.InitPayload({asset: asset, reward: reward, limit: limit}));
    }

    function _makeFungibleTransfer(ABudget.AssetType assetType, address asset, address target, uint256 value)
        internal
        pure
        returns (bytes memory)
    {
        ABudget.Transfer memory transfer;
        transfer.assetType = assetType;
        transfer.asset = asset;
        transfer.target = target;
        transfer.data = abi.encode(ABudget.FungiblePayload({amount: value}));

        return abi.encode(transfer);
    }
}
```

### Mitigation

Handle the claimAmount calculation depending on the asset decimal format used. 

In `ERC20VariableIncentive.sol`

Import this:
```solidity
import {ERC20} from "@solady/tokens/ERC20.sol";
```

Change https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20VariableIncentive.sol#L72 to

```solidity
claimAmount = reward * signedAmount / 10 ** ERC20(asset).decimals();
```