Joyous Satin Gecko

High

# Funds can be permanently stuck due to lack of implementation in BoostOwner contract

### Summary

In Incentive contract there is a `clawback` function that can only be called by the owner of that contract. In every scenario the contract owner is the BoostCore contract. BoostCore contract does not implement any function that allows calling `clawback` in Incentive contract. Since this function can't be called, 100% of the tokens can be stuck in the Incentive contract.

### Root Cause

BoostCore does not implement any function that allows calling `clawback` function in Incentive contrct.

Incentive contracts are initialized by BoostCore in `_makeIncentives` function.

```javascript
function _makeIncentives(BoostLib.Target[] memory targets_, ABudget budget_)
        internal
        returns (AIncentive[] memory incentives)
    {
        incentives = new AIncentive[](targets_.length);
        for (uint256 i = 0; i < targets_.length; i++) {
            // Deploy the clone, but don't initialize until it we've preflighted
            _checkTarget(type(AIncentive).interfaceId, targets_[i].instance);

            // Ensure the target is a base implementation (incentive clones are not reusable)
            if (!targets_[i].isBase) {
                revert BoostError.InvalidInstance(type(AIncentive).interfaceId, targets_[i].instance);
            }

            incentives[i] = AIncentive(_makeTarget(type(AIncentive).interfaceId, targets_[i], false));

            bytes memory preflight = incentives[i].preflight(targets_[i].parameters);
            if (preflight.length != 0) {
                // wake-disable-next-line reentrancy (false positive, entrypoint is nonReentrant)
                assert(budget_.disburse(preflight));
            }

            // wake-disable-next-line reentrancy (false positive, entrypoint is nonReentrant)
            incentives[i].initialize(targets_[i].parameters);
        }
    }
```

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L289

In `initialize` function the owner is initialized with `msg.sender` which in this scenario is `BoostCore` contract.

```javascript
function initialize(bytes calldata data_) public override initializer {
        InitPayload memory init_ = abi.decode(data_, (InitPayload));

        uint256 available = init_.asset.balanceOf(address(this));
        if (available < init_.totalBudget) {
            revert BoostError.InsufficientFunds(init_.asset, available, init_.totalBudget);
        }

        if (
            init_.initialReward == 0 || init_.rewardDecay == 0 || init_.rewardBoost == 0
                || init_.totalBudget < init_.initialReward
        ) revert BoostError.InvalidInitialization();

        asset = init_.asset;
        cgdaParams = CGDAParameters({
            rewardDecay: init_.rewardDecay,
            rewardBoost: init_.rewardBoost,
            lastClaimTime: block.timestamp,
            currentReward: init_.initialReward
        });

        totalBudget = init_.totalBudget;
        _initializeOwner(msg.sender);
    }
```

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/CGDAIncentive.sol#L63

`clawback` implements onlyOwner modifier which means that only `BoostCore` can call that function.

```javascript
function clawback(bytes calldata data_) external virtual override onlyOwner returns (bool) {
        ClawbackPayload memory claim_ = abi.decode(data_, (ClawbackPayload));
        (uint256 amount) = abi.decode(claim_.data, (uint256));

        // Transfer the tokens back to the intended recipient
        asset.safeTransfer(claim_.target, amount);
        emit Claimed(claim_.target, abi.encodePacked(asset, claim_.target, amount));

        return true;
    }
```

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/CGDAIncentive.sol#L103-L112

### Internal pre-conditions

1. Boost needs to be created by boost creator

### External pre-conditions

None

### Attack Path

None, `BoostCore` can't call `clawback` function.

### Impact

100% of the tokens can be stuck in Incentive contract because `BoostCore` can't withdraw them.

### PoC

Because the BoostCore initializes the Incentives contract, he can't call these functions:

`clawback` in:

CGDAIncentive: https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/CGDAIncentive.sol#L103-L112

ERC20Incentive: https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L98-L117

ERC20VariableIncentive: https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20VariableIncentive.sol#L98-L109

ERC1155Incentive: https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC1155Incentive.sol#L104-L118

`drawRaffle` in ERC20Incentive: https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L137-L146

Because `drawRaffle` can't be called, they winner of the raffle will never be drawn and the funds will be stuck in the contract.

### Mitigation

Implement new functions that allows BoostCore call `clawBack` and `drawRaffle`.