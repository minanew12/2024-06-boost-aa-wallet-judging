Powerful Onyx Urchin

High

# Raffles can't be drawn due to incorrectly set access control

### Summary


The main purpose of the boost protocol is to allow users to create different boosts with different incentive models, via the [createBoost()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L106-L144) function. The [createBoost()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L106-L144) function internally calls the [_makeIncentives()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L266-L291) function:

```solidity
    function _makeIncentives(BoostLib.Target[] memory targets_, ABudget budget_)
        internal
        returns (AIncentive[] memory incentives)
    {
        incentives = new AIncentive[](targets_.length);
        for (uint256 i = 0; i < targets_.length; i++) {
            // Deploy the clone, but don't initialize until it we've preflighted
            _checkTarget(type(AIncentive).interfaceId, targets_[i].instance);

            // Ensure the target is a base implementation (incentive clones are not reusable)
            if (!targets_[i].isBase) {
                revert BoostError.InvalidInstance(type(AIncentive).interfaceId, targets_[i].instance);
            }

            incentives[i] = AIncentive(_makeTarget(type(AIncentive).interfaceId, targets_[i], false));

            bytes memory preflight = incentives[i].preflight(targets_[i].parameters);
            if (preflight.length != 0) {
                // wake-disable-next-line reentrancy (false positive, entrypoint is nonReentrant)
                assert(budget_.disburse(preflight));
            }

            // wake-disable-next-line reentrancy (false positive, entrypoint is nonReentrant)
            incentives[i].initialize(targets_[i].parameters);
        }
    }
```
As can be seen from the above code snippet the provided address for the contract has to be a base implementation not an already deployed clone of that implementation (Minimal Proxy). The ``BoostCore.sol`` contract will deploy a clone of the base implementation of the incentives contract that is provided as an argument, and will then become the owner of the Incentive contract. The ``ERC20Incentive.sol`` contract also implements a raffle type of claim, where a random number is drawn, and the winner receives the whole reward of the incentive. The [ERC20Incentive::initialize()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L36-L53) function:
```solidity
    function initialize(bytes calldata data_) public override initializer {
        InitPayload memory init_ = abi.decode(data_, (InitPayload));

        if (init_.reward == 0 || init_.limit == 0) revert BoostError.InvalidInitialization();

        // Ensure the maximum reward amount has been allocated
        uint256 maxTotalReward = init_.strategy != Strategy.RAFFLE ? init_.reward * init_.limit : init_.reward;
        uint256 available = init_.asset.balanceOf(address(this));
        if (available < maxTotalReward) {
            revert BoostError.InsufficientFunds(init_.asset, available, maxTotalReward);
        }

        asset = init_.asset;
        strategy = init_.strategy;
        reward = init_.reward;
        limit = init_.limit;
        _initializeOwner(msg.sender);
    }
```
As we can see from the above code snippet the sender of the initialize call will become the owner of the contract, and as we can see from the [_makeIncentives()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L266-L291) function, the [ERC20Incentive::initialize()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L36-L53) function is called thus the owner of the incentive contract will be the ``BoostCore.sol`` contract. In order for the draw to be drawn the [drawRaffle()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L137-L146) function has to be called. As mentioned before the owner of the clone instance of the ``ERC20Incentive.sol`` contract will be the ``BoostCore.sol`` contract. The ``BoostCore.sol`` contract doesn't implement any functionality that allows someone to call the [ERC20Incentive::drawRaffle()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L137-L146) function, thus the function can never be called. The ``ERC20Incentive.sol`` contract allows it owner to call the [clawback()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L98-L117) function, a withdraw an arbitrary amount of funds: 
```solidity
    function clawback(bytes calldata data_) external override onlyOwner returns (bool) {
        ...
        if (strategy == Strategy.RAFFLE) {
            // Ensure the amount is the full reward and there are no raffle entries, then reset the limit
            if (amount != reward || claims > 0) revert BoostError.ClaimFailed(msg.sender, abi.encode(claim_));
            limit = 0;
        } 
        ...
    }
```
As can be seen if the strategy is **RAFFLE** and one claim has been done funds can't be withdrawn, as the function will revert, and the only way the funds from the contract are distributed is by calling the  [drawRaffle()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L137-L146) function, however as described above that won't be possible. 

### Root Cause

The ``BoostCore.sol`` contract is the owner of the ``ERC20Incentive.sol`` contract, and it doesn't implement the [ERC20Incentive::drawRaffle()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L137-L146) function. 

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

If the ``ERC20Incentive`` contract has the RAFFLE strategy set, the raffle can never be drawn, this is a core functionality of the protocol.  If there is a single claim, the owner of the contract can't withdraw the funds that were deposited to that contract and that will lead to the funds being stuck in the contract forever. Note that the [clawback()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L98-L117) function, that is responsible for withdrawing the funds in the contract, can't be called as well, because it also has the **onlyOwner** modifier. 

### PoC

_No response_

### Mitigation

_No response_