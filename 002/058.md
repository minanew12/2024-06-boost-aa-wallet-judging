Spare Merlot Manatee

High

# Authentication issue will cause the inability of drawing a raffle winner and funds to get stuck in the incentive

### Summary

When creating a boost, the creator can specify the **components** (i.e. Action, Validator, Incentive, etc.) needed for the boost to operate properly. When the components are specified the `BoostCore` contract has to clone and initialize them. When initializing the `Incentive` (say ERC20Incentive) its **owner** becomes the `BoostCore` contract. In the `ERC20Incentive` contract the function `drawRaffle` is callable **only** by the **owner** which in this case is the `BoostCore` contract. However in `BoostCore.sol` there is no way of invoking `ERC20Incentive::drawRaffle` and the raffle winner **cannot** be drawn.

The funds taken from the `Budget` and allocated in the `Incentive` will get **stuck** due to the **inability** to invoke the `ERC20Incentive::clawback` due to the **same** authentication issue found in `ERC20Incentive::drawRaffle`.

### Root Cause

As we can see [here](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L137) the `ERC20Incentive::drawRaffle` is callable **only** by the owner.
The **same** modifier is applied to `ERC20Incentive::clawback`
The owner (set [here](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L52C9-L52C38)) is the `BoostCore` contract.
However nowhere in `BoostCore.sol` can be seen the invocation of `ERC20Incentive::drawRaffle` neither of `ERC20Incentive::clawback`.

### Internal pre-conditions

1. The boost creator creates a boost which holds `ERC20Incentive`
2. The **Target's** `isBase` flag must be set to **true**
3. The `ERC20Incentive`'s **strategy** must be set as a `RAFFLE`

### External pre-conditions

_No response_

### Attack Path

1. The creator creates a `Boost` which holds `ERC20Incentive`
2. The owner of the `Incentive` becomes the `BoostCore` contract
4. `ERC20Incentive::clawback` cannot be invoked and the incentive funds get **stuck**
5. Users claim there participation in the **raffle** after completing the `Action` specified in the `Boost`
6. `ERC20Incentive::drawRaffle` cannot be invoked

### Impact

The described issue disrupts the intended functionality of the protocol as it causes the inability of drawing a raffle winner.
The funds taken from the `Budget` stay stuck in the `Incentive` because of the inability to invoke `ERC20Incentive::clawback` due to the same authentication issue.

### PoC

// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.24;

import {Test, console} from "lib/forge-std/src/Test.sol";
import {MockERC20, MockERC721, MockAuth} from "contracts/shared/Mocks.sol";

import {LibClone} from "@solady/utils/LibClone.sol";
import {LibZip} from "@solady/utils/LibZip.sol";

// Actions
import {AAction} from "contracts/actions/AAction.sol";
import {AContractAction, ContractAction} from "contracts/actions/ContractAction.sol";
import {ERC721MintAction} from "contracts/actions/ERC721MintAction.sol";

// Allowlists
import {AAllowList} from "contracts/allowlists/AAllowList.sol";
import {SimpleAllowList} from "contracts/allowlists/SimpleAllowList.sol";

// Budgets
import {ABudget} from "contracts/budgets/ABudget.sol";
import {SimpleBudget} from "contracts/budgets/SimpleBudget.sol";

// Incentives
import {AIncentive} from "contracts/incentives/AIncentive.sol";
import {ERC20Incentive} from "contracts/incentives/ERC20Incentive.sol";
import {AERC20Incentive} from "contracts/incentives/AERC20Incentive.sol";

// Validators
import {AValidator} from "contracts/validators/AValidator.sol";
import {SignerValidator} from "contracts/validators/SignerValidator.sol";

// Core and Shared
import {BoostCore} from "contracts/BoostCore.sol";
import {BoostRegistry} from "contracts/BoostRegistry.sol";
import {BoostError} from "contracts/shared/BoostError.sol";
import {BoostLib} from "contracts/shared/BoostLib.sol";
import {ACloneable} from "contracts/shared/ACloneable.sol";

import {Ownable} from "@solady/auth/Ownable.sol";

contract BoostCoreTest is Test {
    using LibClone for address;

    MockERC20 mockERC20 = new MockERC20();
    MockERC721 mockERC721 = new MockERC721();
    MockAuth mockAuth;
    address[] mockAddresses;

    BoostCore boostCore = new BoostCore(new BoostRegistry(), address(1));
    BoostLib.Target action = _makeAction(address(mockERC721), MockERC721.mint.selector, mockERC721.mintPrice());
    BoostLib.Target allowList = _makeAllowList(address(this));

    address[] authorized = [address(boostCore)];
    ABudget budget = _makeBudget(address(this), authorized);

    function setUp() public {
        mockERC20.mint(address(this), 100 ether);
        mockERC20.approve(address(budget), 100 ether);
        budget.allocate(
            abi.encode(
                ABudget.Transfer({
                    assetType: ABudget.AssetType.ERC20,
                    asset: address(mockERC20),
                    target: address(this),
                    data: abi.encode(ABudget.FungiblePayload({amount: 100 ether}))
                })
            )
        );
        mockAddresses.push(address(this));
        mockAuth = new MockAuth(mockAddresses);
    }

    struct ClawbackPayload {
        address target;
        bytes data;
    }

    function test_poc_cannot_clawback() public {
        BoostLib.Boost memory boost = boostCore.createBoost(validCreateCalldata);

        address claimee = makeAddr("claimee");
        bytes memory amount_ = abi.encode(100 ether);
        bytes memory payload = abi.encode(ClawbackPayload({target: claimee, data: amount_}));
        
        vm.expectRevert(abi.encodeWithSelector(Ownable.Unauthorized.selector));
        boost.incentives[0].clawback(payload);
    }

    bytes validCreateCalldataRaffle = LibZip.cdCompress(
        abi.encode(
            BoostCore.InitPayload({
                budget: budget,
                action: action,
                validator: BoostLib.Target({isBase: true, instance: address(0), parameters: ""}),
                allowList: allowList,
                incentives: _makeIncentivesRaffle(1),
                protocolFee: 500, // 5%
                referralFee: 1000, // 10%
                maxParticipants: 10_000,
                owner: address(1)
            })
        )
    );

    function _makeIncentivesRaffle(uint256 count) internal returns (BoostLib.Target[] memory) {
        BoostLib.Target[] memory incentives = new BoostLib.Target[](count);
        for (uint256 i = 0; i < count; i++) {
            incentives[i] = BoostLib.Target({
                isBase: true,
                instance: address(new ERC20Incentive()),
                parameters: abi.encode(
                    ERC20Incentive.InitPayload({
                        asset: address(mockERC20),
                        strategy: AERC20Incentive.Strategy.RAFFLE,
                        reward: 1 ether,
                        limit: 100
                    })
                )
            });
        }
        return incentives;
    }

    function test_poc_raffle_not_drawn() public {
        BoostLib.Boost memory boost = boostCore.createBoost(validCreateCalldataRaffle);

        address user = makeAddr("user");
        deal(user, 100 ether);

        vm.mockCall(
            address(boost.action),
            abi.encodeWithSelector(ERC721MintAction.validate.selector),
            abi.encode(true)
        );
        vm.startPrank(user);
        boostCore.claimIncentive{value: 0.000075 ether}(0, 0, address(0), "");
        vm.stopPrank();

        address erc20Incentive = address(boost.incentives[0]);
        vm.expectRevert(abi.encodeWithSelector(Ownable.Unauthorized.selector));
        ERC20Incentive(erc20Incentive).drawRaffle();
    }
}

### Mitigation

The described issue could be resolved by adding **additional role** to the `Incentive` (say admin) which can be set to the **boost creator** and replacing the `onlyOwner` modifier with `onlyRoles` modifier.

The `drawRaffle` invoking inability can also be resoved by making the function callable by anyone only when a certain **theshold** of minimum claims count is reached.