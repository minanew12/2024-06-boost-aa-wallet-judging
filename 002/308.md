Oblong Marigold Blackbird

High

# Ownership flaw in Boost incentive contracts blocks clawback functionality

## Summary

A vulnerability exists in the Boost project's Incentive contracts due to the `clawback` function being restricted by the `onlyOwner` modifier. When BoostCore initializes these contracts during Boost creation, it becomes the owner, preventing the actual Boost owner (who deployed the Boost) from using the `clawback` functionality. This design flaw prevents the Boost owner from reclaiming unused or misallocated assets when necessary.

## Vulnerability Detail

The issue arises because the Incentive contracts are initialized by the BoostCore contract, making it the owner of those contracts. Here's how it occurs: When a user deploys a Boost to advertise their project, they call the `BoostCore::createBoost` function. This function handles several steps to create the Boost, including initializing the Incentive contracts.

```solidity
function createBoost(bytes calldata data_)
        external
        canCreateBoost(msg.sender)
        nonReentrant
        returns (BoostLib.Boost memory)
    {
        InitPayload memory payload_ = abi.decode(data_.cdDecompress(), (InitPayload));

        ...
        // Setup the Boost components
        boost.action = AAction(_makeTarget(type(AAction).interfaceId, payload_.action, true));
        boost.allowList = AAllowList(_makeTarget(type(AAllowList).interfaceId, payload_.allowList, true));
@>      boost.incentives = _makeIncentives(payload_.incentives, payload_.budget);
        ...
    }
```

The `_makeIncentives` function is responsible for setting up the Incentive contracts.

```solidity 
function _makeIncentives(BoostLib.Target[] memory targets_, ABudget budget_)
    internal
    returns (AIncentive[] memory incentives)
{
    incentives = new AIncentive[](targets_.length);
    for (uint256 i = 0; i < targets_.length; i++) {
        _checkTarget(type(AIncentive).interfaceId, targets_[i].instance);
        if (!targets_[i].isBase) {
            revert BoostError.InvalidInstance(type(AIncentive).interfaceId, targets_[i].instance);
        }
        incentives[i] = AIncentive(_makeTarget(type(AIncentive).interfaceId, targets_[i], false));
        bytes memory preflight = incentives[i].preflight(targets_[i].parameters);
        if (preflight.length != 0) {
            assert(budget_.disburse(preflight));
        }
@>      incentives[i].initialize(targets_[i].parameters);
    }
}
```

In this function, after several checks and validations, the `initialize` method is called for each Incentive contract (`incentives[i].initialize(targets_[i].parameters);`). 

Here's how the `initialize` function works, we will use `ERC20Incentive` for the example:

```solidity
function initialize(bytes calldata data_) public override initializer {
    InitPayload memory init_ = abi.decode(data_, (InitPayload));

    if (init_.reward == 0 || init_.limit == 0) revert BoostError.InvalidInitialization();

    uint256 maxTotalReward = init_.strategy != Strategy.RAFFLE ? init_.reward * init_.limit : init_.reward;
    uint256 available = init_.asset.balanceOf(address(this));
    if (available < maxTotalReward) {
        revert BoostError.InsufficientFunds(init_.asset, available, maxTotalReward);
    }

    asset = init_.asset;
    strategy = init_.strategy;
    reward = init_.reward;
    limit = init_.limit;
@>  _initializeOwner(msg.sender);
}
```

The `_initializeOwner(msg.sender)` line sets the owner of the contract, which, in this case, is the BoostCore contract.

Next, let's look at the `clawback` function in the Incentive contracts:

```solidity
@> function clawback(bytes calldata data_) external override onlyOwner returns (bool) {
    ClawbackPayload memory claim_ = abi.decode(data_, (ClawbackPayload));
    ...
}
```

This function is gated by the `onlyOwner` modifier, meaning it can only be called by the owner of the Incentive contract (BoostCore). However, BoostCore lacks any function that allows the Boost deployer (the intended owner) to call the `clawback` function, rendering it unusable.

## Impact

This vulnerability results in the malfunction of one of the protocol's key functionalities. The Boost deployer is unable to reclaim unused or misallocated assets when needed, which could lead to financial losses or inefficiencies in asset management.

## Code Snippet

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L76-L95

## Tools Used

VSCode

## Recommendation

There are two potential solutions to resolve this issue:

1. **Add a `clawback` function to BoostCore:**
   This function would allow the Boost owner to invoke `clawback` on the Incentive contracts via BoostCore.

   ```solidity
   function clawbackIncentive(uint256 incentiveId, bytes calldata data_) external onlyOwner {
       incentives[incentiveId].clawback(data_);
   }
   ```

   This approach preserves access control by ensuring that only the Boost deployer (the owner of the Boost) can call the `clawbackIncentive` function, while still providing access to the Incentive contracts.

2. **Introduce an `incentiveOwner` variable in Incentive contracts:**
   Instead of relying on the `onlyOwner` modifier, introduce an `incentiveOwner` variable and a new `onlyIncentiveOwner` modifier. This would allow the Boost deployer to interact directly with the Incentive contracts, bypassing BoostCore's ownership.

   ```solidity
   address public incentiveOwner;

   modifier onlyIncentiveOwner() {
       require(msg.sender == incentiveOwner, "Not incentive owner");
       _;
   }

   function initialize(bytes calldata data_, address owner_) public initializer {
       incentiveOwner = owner_;
       // Other initialization logic
   }

   function clawback(bytes calldata data_) external onlyIncentiveOwner returns (bool) {
       // Clawback logic
   }
   ```

Both solutions provide the necessary access control while ensuring that the Boost deployer can reclaim assets as needed.