Square Amber Hippo

High

# Incentives `clawback` and `drawRaffle` functions are inaccessible to all parties, including Boost creators and Boost owners.

### Summary

Incentive contracts deployed under the `BoostCore` contract are inaccessible for critical functions due to the access control imposed by the `onlyOwner` modifier. The `BoostCore` contract, set as the owner during initialization, cannot directly interact with functions such as `clawback` and `drawRaffle`, leading to a functional deadlock where no party can reclaim funds or draw raffles. This issue affects the following incentive contracts:

- `CGDAIncentive`: `clawback` function is inaccessible
- `ERC20Incentive`: `clawback` and `drawRaffle` functions are inaccessible
- `ERC20VariableIncentive`: `clawback` function is inaccessible
- `ERC1155Incentive`: `clawback` function is inaccessible

### Root Cause

The root cause of this issue is the access control imposed by the `onlyOwner` modifier on the functions (`clawback`, `drawRaffle`) within the incentive contracts.

During initialization, these contracts are set with the `BoostCore` contract as the owner, preventing direct access to these functions by any other parties, including Boost creators and owners.

When a boost is created using `BoostCore::createBoost`, the BoostCore contract sets itself as the owner of the deployed incentive contract clones through the `initialize` function. This initialization process is uniform across all incentive contracts (`CGDAIncentive`, `ERC20Incentive`, `ERC20VariableIncentive`, `ERC1155Incentive`), making `BoostCore` the owner and sole authorized caller of `onlyOwner` functions.

The `onlyOwner` modifier is used in these incentive contracts to restrict access to `clawback` and `drawRaffle`. While this access control mechanism works for functions intended to be called by the `BoostCore` contract only (e.g., `claim`), it becomes problematic for functions that require interaction from external parties like Boost creators or owners.

### Internal pre-conditions

A Boost is set with any of the following contracts as the Boost incentive:

- `CGDAIncentive`
- `ERC20Incentive`
- `ERC20VariableIncentive`
- `ERC1155Incentive`

### External pre-conditions

_No response_

### Attack Path

The vulnerability path is demonstrated using the `ERC20Incentive` contract. The same path can be followed for the `CGDAIncentive`, `ERC20VariableIncentive`, and `ERC1155Incentive` contracts.

`BoostCore::createBoost` -> `ERC20Incentive::initialize` 

1. User calls `BoostCore::createBoost` passing the compressed data for the Boost:
   
   `(ABudget, Target<AAction>, Target<Validator>, Target<AAllowList>, Target<AIncentive>[], protocolFee, referralFee, maxParticipants, owner)` 
   
   where the `Target<AIncentive>[]` is an array that contains the `Target` struct:

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/shared/BoostLib.sol#L33-L37

```solidity
    struct Target {
        bool isBase;
        address instance;
        bytes parameters;
    }
```

In such `Target`, the `instance` is the `ERC20Incentive` contract address.

After validating the Budget and initializing the Boost, the `createBoost` function sets the Boost components, including the `incentives` array:

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L128

`boost.incentives = _makeIncentives(payload_.incentives, payload_.budget);`

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L266-L291

```solidity
    function _makeIncentives(BoostLib.Target[] memory targets_, ABudget budget_)
        internal
        returns (AIncentive[] memory incentives)
    {
        incentives = new AIncentive[](targets_.length);
        for (uint256 i = 0; i < targets_.length; i++) {
            // Deploy the clone, but don't initialize until it we've preflighted
            _checkTarget(type(AIncentive).interfaceId, targets_[i].instance);

            // Ensure the target is a base implementation (incentive clones are not reusable)
            if (!targets_[i].isBase) {
                revert BoostError.InvalidInstance(type(AIncentive).interfaceId, targets_[i].instance);
            }

@>            incentives[i] = AIncentive(_makeTarget(type(AIncentive).interfaceId, targets_[i], false));

            bytes memory preflight = incentives[i].preflight(targets_[i].parameters);
            if (preflight.length != 0) {
                // wake-disable-next-line reentrancy (false positive, entrypoint is nonReentrant)
                assert(budget_.disburse(preflight));
            }

            // wake-disable-next-line reentrancy (false positive, entrypoint is nonReentrant)
@>            incentives[i].initialize(targets_[i].parameters);
        }
    }
```

The `ERC20Incentive` contract instance passed in the `Target` struct is cloned:

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L254-L260

```solidity
    function _makeTarget(bytes4 interfaceId, BoostLib.Target memory target, bool shouldInitialize)
        internal
        returns (address instance)
    {
        _checkTarget(interfaceId, target.instance);
@>        instance = _maybeClone(target, shouldInitialize);
    }
```

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L294-L300

```solidity
    function _maybeClone(BoostLib.Target memory target_, bool shouldInitialize_) internal returns (address instance) {
@>        instance = target_.isBase ? target_.instance.clone() : target_.instance;
        if (target_.isBase && shouldInitialize_) {
            // wake-disable-next-line reentrancy (false positive, entrypoint is nonReentrant)
            ACloneable(instance).initialize(target_.parameters);
        }
    }
```

2. The BoostCore contract calls the `initialize` function on the `ERC20Incentive` contract clone:

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L36-L53

```solidity
    function initialize(bytes calldata data_) public override initializer {
        InitPayload memory init_ = abi.decode(data_, (InitPayload));

        if (init_.reward == 0 || init_.limit == 0) revert BoostError.InvalidInitialization();

        // Ensure the maximum reward amount has been allocated
        uint256 maxTotalReward = init_.strategy != Strategy.RAFFLE ? init_.reward * init_.limit : init_.reward;
        uint256 available = init_.asset.balanceOf(address(this));
        if (available < maxTotalReward) {
            revert BoostError.InsufficientFunds(init_.asset, available, maxTotalReward);
        }

        asset = init_.asset;
        strategy = init_.strategy;
        reward = init_.reward;
        limit = init_.limit;
@>        _initializeOwner(msg.sender);
    }
```

As the `initialize` function call is made from the `BoostCore` contract, the `msg.sender` is the `BoostCore` contract address. As a result, the `BoostCore` contract is set as the owner of the `ERC20Incentive` contract clone, and the `onlyOwner` modifier in the `ERC20Incentive` contract makes the `clawback` and `drawRaffle` functions inaccessible to all parties. There is no way to call these functions from the `BoostCore` contract.

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L98

`function clawback(bytes calldata data_) external override onlyOwner returns (bool) {`

https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/incentives/ERC20Incentive.sol#L137

`function drawRaffle() external override onlyOwner`

### Impact

In `CGDAIncentive`, `ERC20Incentive`, `ERC20VariableIncentive`, and `ERC1155Incentive` contracts, the `clawback` function becomes inaccessible due to:

1. The `BoostCore` contract being set as the owner of the incentive contract clone
2. The `onlyOwner` modifier in the `clawback` function

Additionally, the `drawRaffle` function in the `ERC20Incentive` contract is also inaccessible due to the same access control mechanism.

### PoC

```solidity
    function testCreateBoostAndClawback() public {
        // address(this) creates the Boost
        BoostLib.Boost memory boost = boostCore.createBoost(validCreateCalldata);
        
        // Check ERC20Incentive owner
        // After creating the boost, the ERC20Incentive clone is initialized and owned by the BoostCore contract
        ERC20Incentive _incentive = ERC20Incentive(address(boost.incentives[0]));
        assertEq(_incentive.owner(), address(boostCore));

        // Prepare the data payload for clawback call
        bytes memory reclaimPayload =
            abi.encode(AIncentive.ClawbackPayload({target: address(1), data: abi.encode(50 ether)}));

        // Boost creator and Boost owner will try to call clawback on the ERC20Incentive contract clone
        // The calls will fail because the `ERC20Incentive` clone is owned by the `BoostCore` contract.
        // Boost creator (address(this)) calls clawback on the ERC20Incentive contract clone
        vm.expectRevert(BoostError.Unauthorized.selector);
        _incentive.clawback(reclaimPayload);

        // Boost owner (address(1)) calls clawback on the ERC20Incentive contract clone
        vm.prank(address(1));
        vm.expectRevert(BoostError.Unauthorized.selector);
        _incentive.clawback(reclaimPayload);

        // Only the `BoostCore` contract can call clawback on the ERC20Incentive clone, but the `BoostCore` contract does not have any function to call `ERC20Incentive::clawback`
        // Therefore, it is impossible for any user to reclaim the funds from the ERC20Incentive contract clone
        // For demonstration, `msg.sender` is set to `BoostCore` contract address for the next clawback function call
        vm.prank(address(boostCore));

        // boostCore address calls clawback on ERC20Incentive contract
        _incentive.clawback(reclaimPayload);

        // The call succeeds and the ERC20Incentive contract transfers the funds to the target address
        assertEq(mockERC20.balanceOf(address(1)), 50 ether);
    }
```

Steps to reproduce:

1. Copy the `testCreateBoostAndClawback` function to `boost-protocol/packages/evm/test/BoostCore.t.sol`
2. From the `boost-protocol/packages/evm` directory, run `forge test --mt testCreateBoostAndClawback` in the terminal.

### Mitigation

Consider implementing a more flexible access control mechanism that allows external parties to interact with the incentive contracts. This can be achieved by introducing a new role, such as an `admin` role, that can access the `clawback` and `drawRaffle` functions. The admin role can be set during the initialization of the incentive contracts and can be used to grant access to these functions.

`AIncentive`

1. Add an `admin` variable to the contract to store the address of the admin.
2. Add a `NotAdmin` error to the contract.
3. Add an `onlyAdmin` modifier to ensure that the caller is the admin.

```diff
@@ -20,6 +20,9 @@ abstract contract AIncentive is IBoostClaim, ACloneable {
     /// @notice Thrown when the incentive is not claimable
     error NotClaimable();

+    /// @notice Thrown when the caller is not the admin
+    error NotAdmin();
+
     /// @notice A struct representing the payload for an incentive claim
     /// @param target The address of the recipient
     /// @param data The implementation-specific data for the claim, if needed

@@ -34,9 +37,18 @@ abstract contract AIncentive is IBoostClaim, ACloneable {
     /// @notice The reward amount issued for each claim
     uint256 public reward;

+    /// @notice The incenvite's admin
+    address public admin;
+
     /// @notice A mapping of address to claim status
     mapping(address => bool) public claimed;

+    /// @notice Modifier to ensure the caller is the admin
+    modifier onlyAdmin() {
+        if (msg.sender != admin) revert NotAdmin();
+        _;
+    }
+
     /// @notice Claim the incentive
     /// @param data_ The data payload for the incentive claim
     /// @return True if the incentive was successfully claimed
```

`ERC20Incentive`

1. Add `address admin` to the `InitPayload` struct.
2. Set the `admin` variable during initialization.
3. Change the `clawback` function modifier to `onlyAdmin`.
4. Change the `drawRaffle` function modifier to `onlyAdmin`.

```diff

@@ -23,6 +23,7 @@ contract ERC20Incentive is AOwnable, AERC20Incentive {
         Strategy strategy;
         uint256 reward;
         uint256 limit;
+        address admin;
     }

     /// @notice Construct a new ERC20Incentive
@@ -49,6 +50,7 @@ contract ERC20Incentive is AOwnable, AERC20Incentive {
         strategy = init_.strategy;
         reward = init_.reward;
         limit = init_.limit;
+        admin = init_.admin;
         _initializeOwner(msg.sender);
     }

@@ -95,7 +97,7 @@ contract ERC20Incentive is AOwnable, AERC20Incentive {
     }

     /// @inheritdoc AIncentive
-    function clawback(bytes calldata data_) external override onlyOwner returns (bool) {
+    function clawback(bytes calldata data_) external override onlyAdmin returns (bool) {
         ClawbackPayload memory claim_ = abi.decode(data_, (ClawbackPayload));
         (uint256 amount) = abi.decode(claim_.data, (uint256));

@@ -134,7 +136,7 @@ contract ERC20Incentive is AOwnable, AERC20Incentive {

     /// @notice Draw a winner from the raffle
     /// @dev Only valid when the strategy is set to `Strategy.RAFFLE`
-    function drawRaffle() external override onlyOwner {
+    function drawRaffle() external override onlyAdmin {
         if (strategy != Strategy.RAFFLE) revert BoostError.Unauthorized();

         LibPRNG.PRNG memory _prng = LibPRNG.PRNG({state: block.prevrandao + block.timestamp});
```

`ERC20VariableIncentive`

1. Add `address admin` to the `InitPayload` struct.
2. Set the `admin` variable during initialization.
3. Change the `clawback` function modifier to `onlyAdmin`.
4. Remove duplicate `_initializeOwner` call.

```diff
@@ -23,6 +23,7 @@ contract ERC20VariableIncentive is AERC20VariableIncentive, AOwnable {
         address asset;
         uint256 reward;
         uint256 limit;
+        address admin;
     }

     /// @notice Construct a new ERC20VariableIncentive
@@ -40,6 +41,7 @@ contract ERC20VariableIncentive is AERC20VariableIncentive, AOwnable {
         address asset_ = init_.asset;
         uint256 reward_ = init_.reward;
         uint256 limit_ = init_.limit;
+        address admin_ = init_.admin;

         if (limit_ == 0) revert BoostError.InvalidInitialization();

@@ -51,9 +53,9 @@ contract ERC20VariableIncentive is AERC20VariableIncentive, AOwnable {
         asset = asset_;
         reward = reward_;
         limit = limit_;
+        admin = admin_;
         totalClaimed = 0;

-        _initializeOwner(msg.sender);
     }

     /// @notice Claim the incentive with variable rewards
@@ -95,7 +97,7 @@ contract ERC20VariableIncentive is AERC20VariableIncentive, AOwnable {
     }

     /// @inheritdoc AIncentive
-    function clawback(bytes calldata data_) external override onlyOwner returns (bool) {
+    function clawback(bytes calldata data_) external override onlyAdmin returns (bool) {
         ClawbackPayload memory claim_ = abi.decode(data_, (ClawbackPayload));
         (uint256 amount) = abi.decode(claim_.data, (uint256));
```

`ERC1155Incentive`

1. Add `address admin` to the `InitPayload` struct.
2. Set the `admin` variable during initialization.
3. Change the `clawback` function modifier to `onlyAdmin`.
4. Remove duplicate `_initializeOwner` call.

```diff
@@ -26,6 +26,7 @@ contract ERC1155Incentive is AOwnable, AERC1155Incentive {
         uint256 tokenId;
         uint256 limit;
         bytes extraData;
+        address admin;
     }

     /// @notice Construct a new ERC1155Incentive
@@ -56,8 +57,7 @@ contract ERC1155Incentive is AOwnable, AERC1155Incentive {
         tokenId = init_.tokenId;
         limit = init_.limit;
         extraData = init_.extraData;
+        admin = init_.admin;
-
-        _initializeOwner(msg.sender);
     }

     /// @inheritdoc AIncentive
@@ -101,7 +101,7 @@ contract ERC1155Incentive is AOwnable, AERC1155Incentive {
     }

     /// @inheritdoc AIncentive
-    function clawback(bytes calldata data_) external override onlyOwner returns (bool) {
+    function clawback(bytes calldata data_) external override onlyAdmin returns (bool) {
         ClawbackPayload memory claim_ = abi.decode(data_, (ClawbackPayload));
         (uint256 amount) = abi.decode(claim_.data, (uint256));
```

`CGDAIncentive`

1. Add `address admin` to the `InitPayload` struct.
2. Set the `admin` variable during initialization.
3. Change the `clawback` function modifier to `onlyAdmin`.

```diff
@@ -28,6 +28,7 @@ contract CGDAIncentive is AOwnable, ACGDAIncentive {
         uint256 rewardDecay;
         uint256 rewardBoost;
         uint256 totalBudget;
+        address admin;
     }

     /// @notice Construct a new CGDAIncentive
@@ -60,6 +61,7 @@ contract CGDAIncentive is AOwnable, ACGDAIncentive {
         });

         totalBudget = init_.totalBudget;
+        admin = init_.admin;
         _initializeOwner(msg.sender);
     }

@@ -100,7 +102,7 @@ contract CGDAIncentive is AOwnable, ACGDAIncentive {
     }

     /// @inheritdoc AIncentive
-    function clawback(bytes calldata data_) external virtual override onlyOwner returns (bool) {
+    function clawback(bytes calldata data_) external virtual override onlyAdmin returns (bool) {
         ClawbackPayload memory claim_ = abi.decode(data_, (ClawbackPayload));
         (uint256 amount) = abi.decode(claim_.data, (uint256));
```