Attractive Mulberry Fly

High

# ERC721MintAction and BoostCore: there's a way to permanently prevent users from claiming their incentives

## Summary
The `ERC721MintAction` overrides the `validate` method that it inherited from the `AValidator` and `AERC721MintAction` contracts:
```solidity
    /// @inheritdoc AValidator
    /// @notice Validate that the action has been completed successfully
    /// @param data_ The data payload for the action `(address holder, (uint256 tokenId))`
    /// @return success True if the action has been validated for the user
    /// @dev The first 20 bytes of the payload must be the holder address and the remaining bytes must be an encoded token ID (uint256)
    /// @dev Example: `abi.encode(address(holder), abi.encode(uint256(tokenId)))`
    function validate(uint256, /*unused*/ uint256, /* unused */ address, /*unused*/ bytes calldata data_)
        external
        virtual
        override
        returns (bool success)
    {
        
        (address holder, bytes memory payload) = abi.decode(data_, (address, bytes));
        uint256 tokenId = uint256(bytes32(payload));

        if (ERC721(target).ownerOf(tokenId) == holder && !validated[tokenId]) {
            validated[tokenId] = true;
            return true;
        } else {
            return false;
        }
    }
```
```solidity
// AValidator.sol
abstract contract AValidator is ACloneable {
    /// @notice Validate that a given user has completed an acction successfully
    /// @param boostId The Id from the available boosts
    /// @param incentiveId The Id from the available boost incentives to be claimed
    /// @param claimant The address of the user claiming the incentive
    /// @param data The encoded payload to be validated
    /// @return True if the action has been validated based on the data payload
    /// @dev The decompressed payload contains freeform bytes that are entirely implementation-specific
    function validate(uint256 boostId, uint256 incentiveId, address claimant, bytes calldata data)
        external
        virtual
        returns (bool);

    /// @inheritdoc ACloneable
    function supportsInterface(bytes4 interfaceId) public view virtual override(ACloneable) returns (bool) {
        return interfaceId == type(AValidator).interfaceId || super.supportsInterface(interfaceId);
    }

    /// @inheritdoc ACloneable
    function getComponentInterface() public pure virtual override(ACloneable) returns (bytes4) {
        return type(AValidator).interfaceId;
    }
}
```


## Vulnerability Detail
Unlike the `SignerValidator` contract, `ERC721MintAction`'s `validate` lacks the `if (msg.sender != _validatorCaller) revert BoostError.Unauthorized();` check, allowing anyone to call `validate`.

***The `ERC721MintAction` is used as the `validator` when a boost of that type and configuration fields gets created via `BoostCore`'s `createBoost` function here:***
```solidity
        boost.validator = AValidator(
            payload_.validator.instance == address(0)
                ? boost.action.supportsInterface(type(AValidator).interfaceId) ? address(boost.action) : address(0)
                : _makeTarget(type(AValidator).interfaceId, payload_.validator, true)
        );
```

There's a problem because if an attacker front-runs the user's `claimIncentiveFor` or `claimIncentive` transaction to the `BoostCore` contract, he can DoS the user from claiming his incentive by blocking the user from proving that he, for instance, has acquired a particular token ID of the collection.

This DoS is unresolveable, and cannot be un-done.

## Impact
So this `BoostCore` function will always revert then on the `if (!boost.validator.validate(boostId_, incentiveId_, claimant, data_)) revert BoostError.Unauthorized();` line:
```solidity
    /// @notice Claim an incentive for a Boost on behalf of another user
    /// @param boostId_ The ID of the Boost
    /// @param incentiveId_ The ID of the AIncentive
    /// @param referrer_ The address of the referrer (if any)
    /// @param data_ The data for the claim
    /// @param claimant the address of the user eligible for the incentive payout
    function claimIncentiveFor(
        uint256 boostId_,
        uint256 incentiveId_,
        address referrer_,
        bytes calldata data_,
        address claimant
    ) public payable nonReentrant {
        BoostLib.Boost storage boost = _boosts[boostId_];
        if (msg.value < claimFee) revert BoostError.InsufficientFunds(address(0), msg.value, claimFee);
        _routeClaimFee(boost, referrer_);

        // wake-disable-next-line reentrancy (false positive, function is nonReentrant)
        if (!boost.validator.validate(boostId_, incentiveId_, claimant, data_)) revert BoostError.Unauthorized();
        if (!boost.incentives[incentiveId_].claim(claimant, data_)) {
            revert BoostError.ClaimFailed(claimant, data_);
        }
        
    }
```

Because `validated[tokenId]` will always be true here: `if (ERC721(target).ownerOf(tokenId) == holder && !validated[tokenId]) {`.

## Code Snippet
https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/d9f597776cc2d20fbb19ffb1f7731126cf3b6210/boost-protocol/packages/evm/contracts/actions/ERC721MintAction.sol#L54-L69

## Tool used
Manual review.

## Recommendation
Consider restricting the `msg.sender` to be the **`_validatedCaller`** by introducing this property in the `ERC721MintAction` contract:
```diff
    function validate(uint256, /*unused*/ uint256, /* unused */ address, /*unused*/ bytes calldata data_)
        external
        virtual
        override
        returns (bool success)
    {
+       require(msg.sender == _validatedCaller, "Not authorized! Bye-bye, replays!!");
        (address holder, bytes memory payload) = abi.decode(data_, (address, bytes));
        uint256 tokenId = uint256(bytes32(payload));

        if (ERC721(target).ownerOf(tokenId) == holder && !validated[tokenId]) {
            validated[tokenId] = true;
            return true;
        } else {
            return false;
        }
    }
```