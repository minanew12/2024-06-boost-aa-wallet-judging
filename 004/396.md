Witty Chrome Starling

Medium

# Funds will be stuck if validator is set to address(0) and action does not support `AValidator` interface

## Summary

In the function `createBoost`, a user can pass through address(0) for the validator. In this case, the action will be set as the validator if it supports the `AValidator` interface.

[BoostCore.sol#L129-L132](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/main/boost-protocol/packages/evm/contracts/BoostCore.sol#L129-L132)
```solidity
boost.validator = AValidator(
            payload_.validator.instance == address(0)
                ? boost.action.supportsInterface(type(AValidator).interfaceId) ? address(boost.action) : address(0)
                : _makeTarget(type(AValidator).interfaceId, payload_.validator, true)
        );
```

`claimIncentiveFor` will attempt to call the validator, but it is address(0) and will always revert. Even though this is technically a user error, the code should not allow this to occur.

## Impact

Any funds that are disbursed from a budget will be locked in the created incentives. Since BoostCore is set as the owner on the incentives, `clawback` cannot be called and the funds will be stuck in the incentive.

## Code Snippet

```solidity
 function testCreateBoostWithNonValidatingAction() public {
        BoostLib.Target memory myAction = _makeNonValidatorAction(
            address(mockERC721),
            MockERC721.name.selector,
            0
        );

        bytes memory createCalldata = LibZip.cdCompress(
            abi.encode(
                BoostCore.InitPayload({
                    budget: budget,
                    action: myAction,
                    validator: BoostLib.Target({isBase: true, instance: address(0), parameters: ""}),
                    allowList: allowList,
                    incentives: _makeIncentives(1),
                    protocolFee: 500, // 5%
                    referralFee: 1000, // 10%
                    maxParticipants: 10_000,
                    owner: address(1)
                })
            )
        );
        boostCore.createBoost(createCalldata);
        address myIncentive = address(boostCore.getBoost(0).incentives[0]);

        vm.expectRevert();
        boostCore.claimIncentive{ value: 0.000075 ether }(0, 0, address(0), "0x");
    }

    ///////////////////////////
    // Test Helper Functions //
    ///////////////////////////

    function _makeNonValidatorAction(address target, bytes4 selector, uint256 value) internal returns (BoostLib.Target memory) {
        return BoostLib.Target({
            isBase: true,
            instance: address(new ContractAction()),
            parameters: abi.encode(
                AContractAction.InitPayload({chainId: block.chainid, target: target, selector: selector, value: value})
            )
        });
    }
```

## Tool used

Manual Review

## Recommendation

Do not allow the validator to be set to address(0).