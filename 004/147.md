Gigantic Alabaster Yak

High

# BoostCore.sol :: If createBoost() is used with an Action that does not support the AValidator interface, the validator address will be set to address(0) and this will cause all claims to revert.

### Summary

**`createBoost()`** is used to create a new boost. The issue arises when an action that doesn't support the **`AValidator`** interface is used, causing the validator address to be set to the zero address. This leads to a problem when **`claimIncentive()`** is called, as it will revert, preventing users from claiming their incentives.

### Root Cause

[createBoost()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/BoostCore.sol#L106-L144) is implemented as follows.
```Solidity
function createBoost(bytes calldata data_)
        external
        canCreateBoost(msg.sender)
        nonReentrant
        returns (BoostLib.Boost memory)
    {
        InitPayload memory payload_ = abi.decode(data_.cdDecompress(), (InitPayload));

        // Validate the Budget
        _checkBudget(payload_.budget);

        // Initialize the Boost
        //@audit-issue BoostCore.sol::protocolFee = Between 0 - FEE_DENOMINATOR and BoostCore.sol::referralFee = Between 0 - FEE_DENOMINATOR is not cheked
        BoostLib.Boost storage boost = _boosts.push();
        boost.owner = payload_.owner;
        boost.budget = payload_.budget;
        boost.protocolFee = protocolFee + payload_.protocolFee;
        boost.referralFee = referralFee + payload_.referralFee;
        boost.maxParticipants = payload_.maxParticipants;

        // Setup the Boost components
        boost.action = AAction(_makeTarget(type(AAction).interfaceId, payload_.action, true));
        boost.allowList = AAllowList(_makeTarget(type(AAllowList).interfaceId, payload_.allowList, true));
        boost.incentives = _makeIncentives(payload_.incentives, payload_.budget);
        boost.validator = AValidator(
            
@>          payload_.validator.instance == address(0)
@>              ? boost.action.supportsInterface(type(AValidator).interfaceId) ? address(boost.action) : address(0)
                : _makeTarget(type(AValidator).interfaceId, payload_.validator, true)
        );
        emit BoostCreated(
            _boosts.length - 1,
            boost.owner,
            address(boost.action),
            boost.incentives.length,
            address(boost.validator),
            address(boost.allowList),
            address(boost.budget)
        );
        return boost;
    }
```
As you can see, if an action does not support the **`AValidator`** interface, the validator address defaults to address(0). This becomes an issue when [claimIncentiveFor()](https://github.com/sherlock-audit/2024-06-boost-aa-wallet/blob/78930f2ed6570f30e356b5529bd4bcbe5194eb8b/boost-protocol/packages/evm/contracts/BoostCore.sol#L164-L180) is called, as it will result in a failure.
```Solidity
function claimIncentiveFor(
        uint256 boostId_,
        uint256 incentiveId_,
        address referrer_,
        bytes calldata data_,
        address claimant
    ) public payable nonReentrant {
        BoostLib.Boost storage boost = _boosts[boostId_];
        if (msg.value < claimFee) revert BoostError.InsufficientFunds(address(0), msg.value, claimFee);
        _routeClaimFee(boost, referrer_);

        // wake-disable-next-line reentrancy (false positive, function is nonReentrant)
@>      if (!boost.validator.validate(boostId_, incentiveId_, claimant, data_)) revert BoostError.Unauthorized();
        if (!boost.incentives[incentiveId_].claim(claimant, data_)) {
            revert BoostError.ClaimFailed(claimant, data_);
        }
    }
```
It calls **`boost.validator.validate()`** to verify the provided data. The issue arises because, in our case, the validator address is set to the zero address, causing all transactions to revert and preventing users from claiming their incentives.

### Internal pre-conditions

The boost creator needs to call **`createBoost()`** with an action that doesn't support the **`AValidator`** interface, for example **`ContractAction`** or **`EventAction`**.

### External pre-conditions

None.

### Attack Path

There is no attack vector because the issue is inherent to the code itself.

### Impact

Users are unable to claim their incentives.

### PoC

To demonstrate the issue, copy the following POC into **`BoostCore.t.sol`**.
```Solidity
import {ContractAction} from "contracts/actions/ContractAction.sol";

function testContractAction_ValidatorAddress0_CreateBoost() public {

        bytes memory validCreateCalldataValidator0 = LibZip.cdCompress(
        abi.encode(
            BoostCore.InitPayload({
                budget: budget,
                //ContractAction does not implement the AValidator interface.
                action: _makeContractAction(),
                validator: BoostLib.Target({isBase: true, instance: address(0), parameters: ""}),
                allowList: allowList,
                incentives: _makeIncentives(1),
                protocolFee: 500, // 5%
                referralFee: 1000, // 10%
                maxParticipants: 10_000,
                owner: address(1)
            })
        )
    );

        BoostLib.Boost memory boost = boostCore.createBoost(validCreateCalldataValidator0);
        assertEq(1, boostCore.getBoostCount());

        //validator is address(0)
        assertEq(address(boost.validator), address(0));

        //Prepare the data payload for validation
        bytes memory data = abi.encode(address(this), "");

        // Claim the incentive
        vm.expectRevert();
        boostCore.claimIncentive{value: 0.000075 ether}(0, 0, address(0), data);
    }

function _makeContractAction() internal returns (BoostLib.Target memory) {
        return BoostLib.Target({
            isBase: true,
            instance: address(new ContractAction()),
            parameters: abi.encode(
                AContractAction.InitPayload({chainId: block.chainid, target: address(1), selector: "", value: 1})
            )
        });
    }
```
Here are the logs:
```Solidity
├─ [51312] BoostCore::claimIncentive{value: 75000000000000}(0, 0, 0x0000000000000000000000000000000000000000, 0x0000000000000000000000007fa9385be102ac3eac297483dd6233d62b3e149600000000000000000000000000000000000000000000000000000000000000400000000000000000000000000000000000000000000000000000000000000000)
    │   ├─ [3000] ECRecover::fallback{value: 37500000000000}()
    │   │   └─ ← [Return] 
    │   ├─ [3000] ECRecover::fallback{value: 37500000000000}()
    │   │   └─ ← [Return] 
    │   ├─ [0] 0x0000000000000000000000000000000000000000::validate(0, 0, BoostCoreTest: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496], 0x0000000000000000000000007fa9385be102ac3eac297483dd6233d62b3e149600000000000000000000000000000000000000000000000000000000000000400000000000000000000000000000000000000000000000000000000000000000)
    │   │   └─ ← [Stop] 
    │   └─ ← [Revert] EvmError: Revert
    └─ ← [Stop]
```
As you can see, the contract attempts to call **^validate()`** on address(0), which causes the transaction to revert.

### Mitigation

To resolve the issue in **`claimIncentiveFor()`**, ensure that the validator address is not the zero address before calling **`validate()`**.
```diff
function claimIncentiveFor(
        uint256 boostId_,
        uint256 incentiveId_,
        address referrer_,
        bytes calldata data_,
        address claimant
    ) public payable nonReentrant {
        BoostLib.Boost storage boost = _boosts[boostId_];
        if (msg.value < claimFee) revert BoostError.InsufficientFunds(address(0), msg.value, claimFee);
        _routeClaimFee(boost, referrer_);

        // wake-disable-next-line reentrancy (false positive, function is nonReentrant)
-       if (!boost.validator.validate(boostId_, incentiveId_, claimant, data_)) revert BoostError.Unauthorized();
+       if(boost.validator != address(0)) {
+          if (!boost.validator.validate(boostId_, incentiveId_, claimant, data_)) revert BoostError.Unauthorized();
+       }
        if (!boost.incentives[incentiveId_].claim(claimant, data_)) {
            revert BoostError.ClaimFailed(claimant, data_);
        }
    }
```